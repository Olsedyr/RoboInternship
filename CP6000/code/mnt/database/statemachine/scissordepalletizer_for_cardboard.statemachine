STATEMACHINEVERSION 3
Name=ScissorDepalletizer3_3
Output=rollerfwd "dummyOut"
Output=rollerrev "dummyOut"
Output=pump "dummyOut"
Output=yv1 "dummyOut"
Output=yv2 "dummyOut"
Output=yv3 "dummyOut"
Output=centre "dummyOut"
Output=scissorSecureReset "dummyOut"
linkValue=palletInPlace test Sc3_3PalletInPlace
Input=palletInPlacePrev "dummyOff"
Input=photoup "dummyOff"
Input=scissorSecure "Scissor Secure"
linkValue=limitdown test Sc3_3PhotoDownLimit
linkValue=limitup test Sc3_3PhotoUpLimit
Link=next PalletConveyor3A
Link=prev PalletConveyor2
Link=parent Parent
Value=finito 0
Value=inTopPos 0
Value=simulate 0
Value=message 0
Value=inReady 0
Const=ON 1
Const=prevON 1
Const=scUpON 1
Const=scissorSecurePol 1
Value=moveIsSafe 1
Value=cPallet 0
Value=upstep 0
Value=inrr 0
Value=errorCode 0
Value=maxPumpTimeOn 0
Timeout=maxDelayStop 10000000
Timeout=StopDelay 100000
Timeout=TimeCentrePos 5200000
Timeout=TimeCentre 3000000
Timeout=MinTimeSearchEdge 500000
Timeout=TimeRevAfterRec 1000000
Timeout=TimeFwdCentre 4000000
Timeout=TimeYV3 7500000
Value=reverseCommand 0
Timeout=maxPumpTime 30000000

; a scissorpalletizer is fast up / std down
; a scissordepalletizer is std up / fast down

State=ST_HALT
END

State=ST_TIMER
END

State=ST_IDLE
  ; set roller 0 before idle. Otherwise PalletReverse cant do it
;  SET rollerfwd 0
;  SET rollerrev 0
  SET pump 0
  SET yv1 0
	SET yv2 0
	SET yv3 0
  SET centre 0
  TEST parent = ST_RESET
  OR simulate = 1
    SET errorCode 0
    SETSTATE ST_RESET
  ELSE
    TEST reverseCommand = 1
			SET inrr 0
			SET centre 0
			SET yv1 1
			SET yv2 1
			SET yv3 0
			TEST limitdown = 1
				SET reverseCommand 2
				SET yv1 0
				SET yv2 0
				SET yv3 0
			ENDTEST
    ENDTEST
  ENDTEST
END

State=ST_RESET
	TEST moveIsSafe = 1
		TEST finito = 0
			TEST limitdown = 1
				TEST palletInPlace = ON
          TEST inReady = 1
            SETSTATE ST_READY
          ELSE
					  SETSTATE ST_STARTCENTRE
          ENDTEST
				ELSE
					SETSTATE ST_EMPTY
				ENDTEST
			ELSE
				TEST inrr = 1
					SETSTATE ST_READY_ROBOT
				ELSE
					TIMEOUT MinTimeSearchEdge ST_SEARCH_EDGE
					SETSTATE ST_SEARCH_EDGE
				ENDTEST
			ENDTEST
		ELSETEST limitdown = 1
		AND palletInPlace != ON
			SET finito 0
			SETSTATE ST_EMPTY
		ELSE
      TIMEOUT TimeYV3 ST_GODOWN_AND_GOREADY
			SETSTATE ST_GODOWN_AND_GOREADY
		ENDTEST
  ELSETEST parent = ST_IDLE
  AND simulate = 0
		SET centre 0
    SET rollerfwd 0
    SET rollerrev 0
    SETSTATE ST_IDLE
	ENDTEST
END

State=ST_SEARCH_EDGE
	SET rollerfwd 0
	SET rollerrev 0
	SET pump 1
	SET yv3 0
	SET yv1 0
	SET yv2 0
	SET centre 0
	TEST photoup = scUpON
	OR limitup = 1
	AND MinTimeSearchEdge = 1
	OR limitup = 1
  AND maxPumpTimeOn = 1
		SET pump 0
		SET yv3 0
		SET upstep 0
		TEST limitup = 1
			SET inTopPos 1
		ELSE
			SET inTopPos 0
		ENDTEST
    SET maxPumpTimeOn 0
		SETSTATE ST_READY_ROBOT
	ENDTEST
  TEST MinTimeSearchEdge = 1
  AND maxPumpTimeOn = 0
    SET maxPumpTimeOn 1
		TIMEOUT maxPumpTime ST_SEARCH_EDGE
    SETSTATE ST_SEARCH_EDGE
  ENDTEST
  TEST scissorSecure = scissorSecurePol
  OR maxPumpTime = 1
  AND maxPumpTimeOn = 1
    SET pump 0
    SET errorCode 3014
    SET rollerfwd 0
    SET rollerrev 0
    SET maxPumpTimeOn 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_READY_ROBOT
	SET inrr 1
	SET centre 1
	TEST limitup = 1
		SET inTopPos 1
	ELSE
		SET inTopPos 0
	ENDTEST
	TEST finito = 1
		SET inrr 0
		SET inTopPos 0
		SET centre 0
    TIMEOUT TimeYV3 ST_GODOWN_AND_GOREADY
		SETSTATE ST_GODOWN_AND_GOREADY
	ELSETEST upstep = 1
		SET inrr 0
		SET centre 0
		TIMEOUT MinTimeSearchEdge ST_SEARCH_EDGE
		SETSTATE ST_SEARCH_EDGE
	ENDTEST
  TEST parent = ST_IDLE
  AND simulate = 0
		SET centre 0
    SET rollerfwd 0
    SET rollerrev 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_GODOWN_AND_GOREADY
	SET rollerfwd 0
	SET rollerrev 0
	SET pump 0
	SET centre 0
	TEST limitdown = 1
		SET finito 0
		SET yv1 0
		SET yv2 0
		SET yv3 0
 		TEST palletInPlace = ON
			SETSTATE ST_READY
		ELSE
			SETSTATE ST_EMPTY
		ENDTEST
	ELSE
		SET yv1 1
		SET yv2 1
    TEST TimeYV3 = 1
      SET yv3 0
    ELSE
		  SET yv3 1
	  ENDTEST
	ENDTEST
END

State=ST_EMPTY
  SET inReady 0
  TEST prev = ST_SEND
    TIMEOUT maxDelayStop ST_RECEIVE
    SETSTATE ST_RECEIVE
  ELSE
    SET rollerfwd 0
  ENDTEST
  TEST limitdown = 0
    SET errorCode 1023
    SET rollerfwd 0
    SET rollerrev 0
    SETSTATE ST_IDLE
  ENDTEST
  TEST parent = ST_IDLE
  AND simulate = 0
    SET rollerfwd 0
    SET rollerrev 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_RECEIVE
  TEST palletInPlace = ON
    TEST palletInPlacePrev != prevON
		  TIMEOUT TimeRevAfterRec ST_REV_AFTER_REC
      SETSTATE ST_REV_AFTER_REC
    ELSE
      SET errorCode 3014
      SET rollerfwd 0
      SET rollerrev 0
      SETSTATE ST_IDLE
    ENDTEST
  ELSETEST maxDelayStop = 1
    SET errorCode 3014
    SET rollerfwd 0
    SET rollerrev 0
    SETSTATE ST_IDLE
  ELSETEST prev = ST_SEND
    SET rollerfwd 1
		SET rollerrev 0
  ENDTEST
  TEST limitdown = 0
    SET errorCode 1023
    SET rollerfwd 0
    SET rollerrev 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_REV_AFTER_REC
	SET rollerfwd 0
	SET rollerrev 1
	TEST TimeRevAfterRec = 1
		SETSTATE ST_STARTCENTRE
	ENDTEST
END

State=ST_READY
  SET inReady 1
  TEST next = ST_EMPTY
    TEST limitdown = 1
      TIMEOUT maxDelayStop ST_SEND
      SETSTATE ST_SEND
    ELSE
      SET errorCode 1023
      SET rollerfwd 0
      SET rollerrev 0
      SETSTATE ST_IDLE
    ENDTEST
  ELSE
    SET rollerfwd 0
    SET rollerrev 0
  ENDTEST
  TEST parent = ST_IDLE
  AND simulate = 0
    SET rollerfwd 0
    SET rollerrev 0
    SETSTATE ST_IDLE
  ENDTEST  
END

State=ST_SEND
  TEST next = ST_READY
  AND palletInPlace != ON
    CALC cPallet = cPallet + 1
    SETSTATE ST_EMPTY
  ELSETEST maxDelayStop = 1
    SET errorCode 3014
    SET rollerfwd 0
    SET rollerrev 0
    SETSTATE ST_IDLE
  ELSE
    SET rollerfwd 1
  ENDTEST
  TEST limitdown = 0
    SET errorCode 1023
    SET rollerfwd 0
    SET rollerrev 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_STARTCENTRE
	SET rollerfwd 0
	SET rollerrev 0
	SET pump 1
	SET yv1 0
	SET yv2 0
	SET yv3 0
  SET centre 0
	TIMEOUT TimeCentrePos ST_CENTRE
	SETSTATE ST_CENTRE
END

State=ST_CENTRE
	TEST TimeCentrePos = 1
		SET rollerfwd 1
		SET rollerrev 0
		SET pump 0
		SET centre 0
		TIMEOUT TimeFwdCentre ST_CENTRE_AND_FWD
		SETSTATE ST_CENTRE_AND_FWD
	ENDTEST
  TEST scissorSecure = scissorSecurePol
    SET pump 0
    SET errorCode 3014
    SET rollerfwd 0
    SET rollerrev 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_CENTRE_AND_FWD
	TEST TimeFwdCentre = 1
		SET rollerfwd 1
		SET rollerrev 0
		SET pump 0
		SET centre 1
		TIMEOUT TimeCentre ST_ENDCENTRE
		SETSTATE ST_ENDCENTRE
	ENDTEST
END

State=ST_ENDCENTRE
	TEST TimeCentre = 1
		SET rollerfwd 0
		SET rollerrev 0
		SET centre 0
		TIMEOUT MinTimeSearchEdge ST_SEARCH_EDGE
		SETSTATE ST_SEARCH_EDGE
	ENDTEST
END
