STATEMACHINEVERSION 3
;Include=cardboardconveyor.statemachine
Name=CardBoardConveyor
Input=induksensor "dummyOff"
Output=motor "dummyOut"
Link=parent Parent
Value=errorCode 0
Value=simulate 0
Value=sheetsPerDispense 3
Value=newsheet 0
Value=sheetcount 0
Value=homed 0
Const=enable 1
Timeout=TimeMinDispense 800000
Timeout=TimeAfterRun 480000
Timeout=TimeHome 10000000

State=ST_HALT
END

State=ST_TIMER
END

State=ST_IDLE
  SET motor 0
  TEST parent = ST_RESET
  AND enable = 1
    SET errorCode 0
    SETSTATE ST_RESET
	ELSETEST newsheet = 1
		SET newsheet 0
  ENDTEST
END

State=ST_RESET
	TEST homed = 0
		TIMEOUT TimeHome ST_HOME
		SETSTATE ST_HOME
	ELSE
		SETSTATE ST_WAIT
	ENDTEST
END

State=ST_HOME
	SET motor 1
	TEST induksensor = 1
		SET homed 1
		TIMEOUT TimeAfterRun ST_AFTERRUN
		SETSTATE ST_AFTERRUN
	ENDTEST
	TEST TimeHome = 1
		SET errorCode 1048 
		SETSTATE ST_IDLE
	ENDTEST
END

State=ST_WAIT
	SET motor 0
	TEST parent = ST_IDLE
	AND simulate = 0
		SETSTATE ST_IDLE
	ELSETEST newsheet = 1
		CALC sheetcount = sheetcount + 1
		TEST sheetcount >= sheetsPerDispense
			TIMEOUT TimeMinDispense ST_DISPENSE
			SETSTATE ST_DISPENSE
		ELSE
			SET newsheet 0
		ENDTEST
	ENDTEST
END

State=ST_DISPENSE
	SET motor 1
	TEST TimeMinDispense = 1
		TEST induksensor = 1
			SET sheetcount 0
			TIMEOUT TimeAfterRun ST_AFTERRUN
			SETSTATE ST_AFTERRUN
		ENDTEST
	ENDTEST
END

State=ST_AFTERRUN
	SET motor 1
	TEST TimeAfterRun = 1
		SET newsheet 0
		SET motor 0
		SETSTATE ST_WAIT
	ENDTEST
END
