STATEMACHINEVERSION 3
Name=WorkCell
Link=Emergency
Link=ContinueKnap
Link=ErrorCode
; Statemachines with output, that should be tested for ST_IDLE
; find them with 'grep -l "Output=" 7XX*.statemachine'
Link=KassePakkerBottom
Link=KassePakkerMotor	
Link=KassePakkerTop
linkValue=newprogram Program
Value=errorCode 0
Value=firstRun 0
Value=doFakeProducts 0
Timeout=timeBeforeRunning 3000000
Timeout=timeBeforePausing 2000000

State=ST_HALT
END

State=ST_IDLE
	SET errorCode 0
  TEST ContinueKnap = ST_HIGH
    TIMEOUT timeBeforeRunning ST_STARTING
    SETSTATE ST_STARTING
  ENDTEST
END

State=ST_STARTING
  SET firstRun 1
	TEST timeBeforeRunning = 1
		SETSTATE ST_RUNNING
	ENDTEST
	TEST Emergency = ST_POWEROFF
		SET errorCode 203
		SETSTATE ST_ERROR
	ENDTEST
END

State=ST_RESTARTING
  SET firstRun 0
	TEST timeBeforeRunning = 1
		SETSTATE ST_RUNNING
	ENDTEST
	TEST Emergency = ST_POWEROFF
		SET errorCode 203
		SETSTATE ST_ERROR
	ENDTEST
END

State=ST_RUNNING
  TEST ContinueKnap = ST_HIGH
    TIMEOUT timeBeforePausing ST_PAUSING
    SETSTATE ST_PAUSING
  ENDTEST
	TEST ErrorCode = ST_ERROR
		TIMEOUT timeBeforePausing ST_PAUSING
    SETSTATE ST_PAUSING
	ENDTEST
	TEST Emergency = ST_POWEROFF
		SET errorCode 203
		SETSTATE ST_ERROR
	ENDTEST
END

State=ST_STOPPING
  SETSTATE ST_IDLE
	TEST Emergency = ST_POWEROFF
		SET errorCode 203
		SETSTATE ST_ERROR
	ENDTEST
END

State=ST_PAUSING
	; dont test for trafficlight :-)
	TEST timeBeforePausing = 1
	AND KassePakkerBottom = ST_IDLE
	AND KassePakkerTop = ST_IDLE
  	AND KassePakkerMotor = ST_IDLE
	SETSTATE ST_PAUSED
	ENDTEST
	TEST Emergency = ST_POWEROFF
		SET errorCode 203
		SETSTATE ST_ERROR
	ENDTEST
END

State=ST_PAUSED
	TEST newprogram = 1
		SETSTATE ST_IDLE
	ELSE
		TEST ContinueKnap = ST_HIGH
      SET firstRun 0
			TIMEOUT timeBeforeRunning ST_RESTARTING
			SETSTATE ST_RESTARTING
		ENDTEST
	ENDTEST
END

State=ST_ERROR
  TEST ContinueKnap = ST_HIGH
		SET errorCode 0
    TIMEOUT timeBeforeRunning ST_STARTING
    SETSTATE ST_STARTING
  ENDTEST
END

State=ST_RESET
END

State=ST_TIMER
END

