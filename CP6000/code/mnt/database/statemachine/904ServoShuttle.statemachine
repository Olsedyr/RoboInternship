STATEMACHINEVERSION 3
Name=ServoShuttle
Output=Mode "Shuttle Mode"
Output=setpoint "Shuttle MacSetpoint"
Output=vsoll "Shuttle Maxvel"
Output=asoll "Shuttle Maxacc"
Output=set_curpos "Shuttle Curpos"
Output=kvout "Shuttle Load Factor"
Output=set_status "Shuttle Status"
Output=minpist "Shuttle Minpos"
Output=maxpist "Shuttle Maxpos"
Input=temperature "Shuttle Temperature"
Input=Inpos "Shuttle IN_POS"
Input=curpos "Shuttle Curpos"
Input=status "Shuttle Status"
Link=Master Shuttle
Link=parent Parent
Link=photo1 ShuttleInPositionPhoto1
Link=photo2 ShuttleInPositionPhoto2
; tickspermotorround*gearudveksling/(eff_rad*2*pi)
; 8000*20.0/(75*2*pi)
Const=tickspermm 339.5305452627100629
linkValue=gotoPosition Shuttle
linkValue=pos3_5mm Shuttle
linkValue=pos3_4mm Shuttle
linkValue=pos4_4mm Shuttle
linkValue=pos4_8mm Shuttle
linkValue=pos4_9mm Shuttle
linkValue=pos5_9mm Shuttle
linkValue=pos5_10mm Shuttle
linkValue=almostHome test ShuttleAlmostHome
Value=curposmm 0
Value=speed 0
Value=dir 0
Value=homed 0
Value=errorCode 0
Value=message 0
Value=curpm 0.0
Value=distance 0.0
Value=startpos 0.0
Value=oldTestPos 0
Value=testPos 0
Value=seenPhoto 0
Value=searching 0
Value=myGotoPos 0
Value=testGotoPos 1500
Value=oldTestGotoPos 1500
Const=maxSearchmm 100
Value=maxSearchTicks 0
Value=searchStartPos 0
Value=temp 0
Value=ftemp 0.0
Value=slipoffset 0
Const=loadfactor 2.0000
Const=home_speed_slow 100.0000
Const=home_speed_fast -1500.0000
Const=home_acc 3000.0000
Const=acceleration 1500.0000
Const=cruise_speed 3000.0000
Const=search_speed 200.0000
Const=maxticks 5100000
Const=minticks -100000
Value=testmode 0
Value=useslipoffset 1
Const=dirpol -1
Const=homeoffset 2103
Timeout=TimeCmdDelay 500000
Timeout=TimeStartMax 1000000
Timeout=TimeHomeSlow 60000000
Timeout=TimeHomeFast 25000000
Value=simulate 0

State=ST_TIMER
END

State=ST_HALT
END

State=ST_IDLE
  SET Mode 0
  CALC curposmm = curpos / tickspermm
  CALC curposmm = dirpol * curposmm
	TEST parent = ST_RESET
  OR testmode = 1
    TEST status != 16
	  AND status != 32
	  AND status != 48
	  AND status != 64
	  AND status != 80
	  AND status != 0
      SET set_status 0
    ENDTEST
    SET errorCode 0
		SETSTATE ST_RESET
	ENDTEST
END

State=ST_RESET
  SET kvout loadfactor
	SET asoll acceleration
  CALC maxSearchTicks = tickspermm * maxSearchmm
  TEST homed = 0
    SETSTATE ST_HOME
	ELSE
    SET vsoll cruise_speed
		SET setpoint curpos
		TIMEOUT TimeCmdDelay ST_CHOOSETRACK
		SETSTATE ST_TIMER
	ENDTEST
END

State=ST_CHOOSETRACK
  ;gotoPosition is valid when Master is in ST_BEGINMOVE
  TEST Master = ST_BEGINMOVE
    SET myGotoPos gotoPosition
    SETSTATE ST_START_CALC
  ELSE
		TEST testmode = 1
			SETSTATE ST_TEST
		ENDTEST
	ENDTEST
	TEST Master = ST_IDLE
  AND testmode = 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_TEST
  TEST oldTestPos != testPos
  OR testGotoPos != oldTestGotoPos
    TEST testPos = 7
      SET myGotoPos pos3_5mm
    ELSETEST testPos = 6
      SET myGotoPos pos3_4mm
    ELSETEST testPos = 5
      SET myGotoPos pos4_4mm
    ELSETEST testPos = 4
      SET myGotoPos pos4_8mm
    ELSETEST testPos = 3
      SET myGotoPos pos4_9mm
    ELSETEST testPos = 2
      SET myGotoPos pos5_9mm
    ELSETEST testPos = 1
      SET myGotoPos pos5_10mm
    ENDTEST
    TEST testGotoPos != oldTestGotoPos
      SET myGotoPos testGotoPos
    ENDTEST
    SET oldTestPos testPos
    SET oldTestGotoPos testGotoPos
	  SETSTATE ST_START_CALC
  ENDTEST
END
  
State=ST_STOP
	SET curpm 1000.0
  SET setpoint curpos
  SET vsoll cruise_speed
  CALC curposmm = curpos / tickspermm
  CALC curposmm = dirpol * curposmm
  TIMEOUT TimeCmdDelay ST_CHOOSETRACK
	SETSTATE ST_TIMER
END

State=ST_START_CALC
  TEST useslipoffset = 1
    CALC myGotoPos = myGotoPos - slipoffset
  ENDTEST
  CALC myGotoPos = dirpol * myGotoPos
  CALC setpoint = tickspermm * myGotoPos
	SET startpos curpos
	CALC distance = setpoint - curpos
  TEST distance < 0
    CALC dir = -1 * dirpol
  ELSE
    CALC dir = 1 * dirpol
  ENDTEST
	SET Mode 2
  TIMEOUT TimeStartMax ST_START
	SETSTATE ST_START
END

State=ST_START
	SET curpm 0.0
	TEST Inpos = 0
	OR TimeStartMax = 1
    SET seenPhoto 0
	  SETSTATE ST_RUN
  ENDTEST
END

State=ST_RUN
	CALC curpm = curpos - startpos
	CALC curpm = curpm / distance
	CALC curpm = curpm * 1000.0  
  CALC curposmm = curpos / tickspermm
  CALC curposmm = dirpol * curposmm
  CALC temp = curpos - setpoint
  TEST temp < 0
    CALC temp = -1 * temp
  ENDTEST
  TEST temp < maxSearchTicks
	  TEST photo1 = ST_BLOCKED
    AND photo2 = ST_BLOCKED
      SET seenPhoto 1
    ENDTEST
  ENDTEST
  TEST Inpos = 1
	  TEST photo1 = ST_BLOCKED
    AND photo2 = ST_BLOCKED
      SET Mode 0
      TIMEOUT TimeCmdDelay ST_STOP
      SETSTATE ST_TIMER
    ELSE
      SET searching 0
      SET vsoll search_speed
      PRINT searching
      TIMEOUT TimeCmdDelay ST_RUN_SEARCH_PHOTO
      SETSTATE ST_TIMER
    ENDTEST
  ENDTEST
  TEST status != 16
	AND status != 32
	AND status != 48
	AND status != 64
	AND status != 80
	AND status != 0
    SET Mode 0
    SET homed 0
    TIMEOUT TimeCmdDelay ST_ERROR
    SETSTATE ST_TIMER
  ENDTEST
  TEST simulate = 1
    SET Mode 0
    TIMEOUT TimeCmdDelay ST_STOP
    SETSTATE ST_TIMER
  ENDTEST
END

State=ST_ERROR
  TEST status != 16
	AND status != 32
	AND status != 48
	AND status != 64
	AND status != 80
	AND status != 0
    SET set_status 0
  ENDTEST
  SETSTATE ST_IDLE
END

State=ST_RUN_SEARCH_PHOTO
  CALC curposmm = curpos / tickspermm
  CALC curposmm = dirpol * curposmm
  TEST searching = 0
  OR searching = 2
    SET searchStartPos curpos
    CALC temp = dirpol * maxSearchTicks
    CALC temp = dir * temp
    TEST seenPhoto = 1
      ;go back
      CALC temp = curpos - temp
    ELSE
      ;go further
      CALC temp = curpos + temp
    ENDTEST
    SET setpoint temp
    CALC searching = searching + 1
    PRINT seenPhoto
  ELSE
    TEST photo1 = ST_BLOCKED
    AND photo2 = ST_BLOCKED
      SET Mode 0
      CALC maxSearchTicks = tickspermm * maxSearchmm
      TIMEOUT TimeCmdDelay ST_SLIP_OFFSET
      SETSTATE ST_TIMER
    ELSE
      CALC temp = curpos - searchStartPos
      TEST temp < 0
        CALC temp = -1 * temp
      ENDTEST
      TEST temp > maxSearchTicks
        TEST searching = 1
          ;try again
          CALC maxSearchTicks = tickspermm * maxSearchmm
          CALC maxSearchTicks = 2 * maxSearchTicks
          CALC dir = -1 * dir
          SET seenPhoto 0
          SET searching 2
        ELSE
          TEST seenPhoto = 1
            ;03601 Fejl: Palleshuttle har været i position, men er efterfølgende kørt forbi fotocellerne.
            SET errorCode 3601
            SET Mode 0
            SET homed 0
            TIMEOUT TimeCmdDelay ST_ERROR
            SETSTATE ST_TIMER
          ELSE
            ;03602 Fejl: Fotoceller på palleshuttle er ikke blevet aktiveret ved ønsket position.
            SET errorCode 3602
            SET Mode 0
            SET homed 0
            TIMEOUT TimeCmdDelay ST_ERROR
            SETSTATE ST_TIMER
          ENDTEST
        ENDTEST
      ENDTEST
    ENDTEST
  ENDTEST
END

State=ST_SLIP_OFFSET
  SET temp slipoffset
  CALC slipoffset = curpos / tickspermm
  CALC slipoffset = myGotoPos - slipoffset
  CALC slipoffset = dirpol * slipoffset
  CALC slipoffset = slipoffset + temp
  PRINT slipoffset
  SETSTATE ST_STOP
END

State=ST_HOME
  TEST almostHome = 0
    CALC speed = dirpol * home_speed_fast
	  SET vsoll speed
    SET asoll home_acc
  ELSE
    CALC speed = dirpol * home_speed_slow
	  SET vsoll speed
  ENDTEST
  SET maxpist 2000000000
  SET minpist -2000000000
	TIMEOUT TimeCmdDelay ST_HOME2
  SETSTATE ST_HOME2
END

State=ST_HOME2
  TEST TimeCmdDelay = 1
    ;03501 Info: Palleshuttle søger hjemmeposition.
    SET message 3501
    SET Mode 1
    TEST almostHome = 0
      TIMEOUT TimeHomeFast ST_HOME_FAST
		  SETSTATE ST_HOME_FAST
    ELSE
      SET asoll acceleration
      TIMEOUT TimeHomeSlow ST_HOME_SLOW
		  SETSTATE ST_HOME_SLOW
    ENDTEST
  ENDTEST
END

State=ST_HOME_FAST
  TEST almostHome = 1
  OR simulate = 1
    CALC speed = dirpol * home_speed_slow
	  SET vsoll speed
    TIMEOUT TimeCmdDelay ST_BEGIN_HOME_SLOW
    SETSTATE ST_TIMER
	ELSE
		TEST TimeHomeFast = 1
			SET errorCode 1049
      SET message 0
      SET Mode 0
      TIMEOUT TimeCmdDelay ST_ERROR
      SETSTATE ST_TIMER
		ENDTEST
  ENDTEST
  TEST status != 16
	AND status != 32
	AND status != 48
	AND status != 64
	AND status != 80
	AND status != 0
    SET Mode 0
    SET message 0
    TIMEOUT TimeCmdDelay ST_ERROR
    SETSTATE ST_TIMER
  ENDTEST
END

State=ST_BEGIN_HOME_SLOW
  SET asoll acceleration
  TIMEOUT TimeHomeSlow ST_HOME_SLOW
	SETSTATE ST_HOME_SLOW
END

State=ST_HOME_SLOW
	TEST almostHome = 0
    SET Mode 0
		TIMEOUT TimeCmdDelay ST_HOME_SLOW2
		SETSTATE ST_HOME_SLOW2
	ELSE
		TEST TimeHomeSlow = 1
			SET errorCode 1049
      SET message 0
      SET Mode 0
      TIMEOUT TimeCmdDelay ST_ERROR
      SETSTATE ST_TIMER
		ENDTEST
	ENDTEST
  TEST status != 16
	AND status != 32
	AND status != 48
	AND status != 64
	AND status != 80
	AND status != 0
    SET Mode 0
    SET message 0
    TIMEOUT TimeCmdDelay ST_ERROR
    SETSTATE ST_TIMER
  ENDTEST
END

State=ST_HOME_SLOW2
	TEST TimeCmdDelay = 1
    PRINT homed
		SET homed 1
    CALC ftemp = tickspermm * homeoffset
    CALC ftemp = dirpol * ftemp
    SET setpoint ftemp
    SET set_curpos ftemp
    SET message 0
    SET vsoll cruise_speed
    SET slipoffset 0
    TEST dirpol = -1
      CALC maxpist = dirpol * minticks
      CALC minpist = dirpol * maxticks
    ELSE
      CALC maxpist = dirpol * maxticks
      CALC minpist = dirpol * minticks
    ENDTEST
		TIMEOUT TimeCmdDelay ST_CHOOSETRACK
		SETSTATE ST_TIMER
	ENDTEST
END
