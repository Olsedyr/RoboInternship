STATEMACHINEVERSION 3
Name=PIDLift
;** Input **
Input=raw "KasseLift Encoder"
Input=powerok "KasseLift HZOK"
;** Output **
Output=motor "KasseLift Setpoint"
Output=encoderenable "KasseLift Input Validation"
Output=encoderreset "KasseLift Direct"
Output=motorenable "KasseLift Motor Enable"
Output=brakerelease "KasseLift Brake Release"
;** Link **
Link=CamGen CamGenLift
Link=parent Parent
;** linkValue **
linkValue=firstRun WorkCell
linkValue=setpoint CamGen
linkValue=setspeed CamGen
linkValue=camgenbeginning beginning CamGen
;** Table **
Table=pehistory 600
;** Item **
;** Const **
Const=maxhomeclicks 35
Const=minheight 0
Const=maxheight 14000
Const=home 2000
Const=Kp 4.0
Const=Kd 15.0
Const=Ki 0.1
Const=Ks 0.0
Const=Kbias 400.0
Const=intdecay 0.9200
Const=minintegral -80000.0
Const=maxintegral 80000.0
Const=minoutval -55.0
Const=maxoutval 55.0
Const=factor 342.0000
Const=encoderdir 1
Const=motordir 1
Const=motorstartval 0.0
Const=slowStopSpeed 0.72
Const=holdSpeed 0.75
Const=maxposerr 1500
Const=maxnegposerr -1500
Const=maxstaticposerr 200
Const=maxstaticnegposerr -200
;** Value **
Value=homeclicks 0
Value=oldsetpoint -10
Value=staticmode 0
Value=encoder 0
Value=outval 0.0
Value=poserr 0.0
Value=oldposerr 0.0
Value=integral 0.0
Value=diff 0.0
Value=Kpart 0.0
Value=Dpart 0.0
Value=Ipart 0.0
Value=Spart 0.0
Value=curpos 0.0
Value=homed 0
Value=errorCode 0
Value=temp 0.0
Value=temp1 0.0
;** Timeout **
Timeout=TimeHomeGo 75000
Timeout=TimeHomeStop 300000
Timeout=TimeHomeEnd 500000
Timeout=TimeSlowStop 1000000
Timeout=TimeStatic 1000000
Timeout=TimeMotorFirstEnable 1000000
Timeout=TimeMotorEnable 250000
Timeout=TimeDisable 100000
Timeout=TimeBrakeRelease 80000

State=ST_TIMER
END

State=ST_HALT
END

State=ST_IDLE
  SET motor 0.0
  SET encoderreset 2
  SET motorenable 0
	SET brakerelease 0
  SET oldsetpoint -10
  SET staticmode 0
	CALC encoder = raw - home
  CALC encoder = encoderdir * encoder
  SET poserr 0.0
  SET integral 0.0
  ; don't SET encoderenable to 0
	TEST CamGen = ST_RESET
  OR CamGen = ST_NOCHOOSETRACK
		SETSTATE ST_RESET
	ENDTEST
  TEST parent = ST_RESET
    SET errorCode 0
  ENDTEST
END

State=ST_SLOWSTOP
	SET motor slowStopSpeed
	TEST TimeSlowStop = 1
		SETSTATE ST_IDLE
	ENDTEST
END

State=ST_RESET
  SET poserr 0.0
  SET integral 0.0
	SET oldsetpoint -10
	SET staticmode 0
	TEST firstRun = 1
		SET homed 0
	ENDTEST
  TEST homed = 1
		SET motorenable 1
		TIMEOUT TimeMotorFirstEnable ST_FIRST_ENABLE_WAIT
    SETSTATE ST_FIRST_ENABLE_WAIT
  ELSE
    SET encoderenable 1
    SETSTATE ST_STARTHOME
  ENDTEST
END

State=ST_FIRST_ENABLE_WAIT
	CALC encoder = raw - home
  CALC encoder = encoderdir * encoder
  TEST TimeMotorFirstEnable = 1
	  SET brakerelease 1
	  TIMEOUT TimeBrakeRelease ST_BRAKERELEASE_WAIT
    SETSTATE ST_BRAKERELEASE_WAIT
  ENDTEST
END

State=ST_RUN
	SET encoderreset 2
	CALC encoder = raw - home
  CALC encoder = encoderdir * encoder
  SET curpos encoder
  SET oldposerr poserr
  CALC poserr = setpoint - curpos
	SET pehistory poserr
  CALC diff = poserr - oldposerr
  CALC integral = poserr + integral
  CALC integral = integral * intdecay
  TEST integral < minintegral
    SET integral minintegral
  ELSE
    TEST integral > maxintegral
      SET integral maxintegral
    ENDTEST
  ENDTEST
  CALC Kpart = Kp * poserr
  CALC Dpart = Kd * diff
  CALC Ipart = Ki * integral
  CALC Spart = Ks * setspeed
  CALC outval = Dpart + Kpart
  CALC outval = Ipart + outval
  CALC outval = Spart + outval
	CALC outval = outval + Kbias
	CALC outval = outval / factor
  TEST outval < minoutval
    SET outval minoutval
  ELSE
    TEST outval > maxoutval
      SET outval maxoutval
    ENDTEST
  ENDTEST
  CALC outval = motordir * outval
	TEST staticmode = 1
		TEST setpoint = oldsetpoint
    AND camgenbeginning = 0
			SET brakerelease 0
      TEST TimeDisable = 1
			  SET motor holdSpeed
        SET motorenable 0
			ELSE
				SET motor outval
      ENDTEST
		ELSE
      TEST camgenbeginning = 1
        SET staticmode 0
        TIMEOUT TimeMotorEnable ST_ENABLE_WAIT
        SETSTATE ST_ENABLE_WAIT
      ENDTEST
		ENDTEST
		SET oldsetpoint setpoint
	ELSE
		SET motor outval
		TEST TimeStatic = 1
			TEST setpoint = oldsetpoint
      AND CamGen != ST_RUN
      ;AND poserr < 200
        TEST poserr > maxstaticposerr
        OR poserr < maxstaticnegposerr
          ;01019 Fejl: Følgefejl på lift.
          SET errorCode 1019
          SET brakerelease 0
          SET motor 0.0
          SET motorenable 0
          SETSTATE ST_WAIT_CAMGEN_IDLE
        ELSE
				  SET staticmode 1
				  SET brakerelease 0
			    TIMEOUT TimeDisable ST_RUN
			    SETSTATE ST_RUN
        ENDTEST
			ELSE
				TEST powerok = 1
					SET brakerelease 1
          SET motorenable 1
				ENDTEST
        TIMEOUT TimeStatic ST_RUN
        SETSTATE ST_RUN
			ENDTEST
			SET oldsetpoint setpoint
		ENDTEST
	ENDTEST
  TEST CamGen = ST_IDLE
		SET brakerelease 0
		TIMEOUT TimeSlowStop ST_SLOWSTOP
		SETSTATE ST_SLOWSTOP
  ENDTEST
  TEST poserr > maxposerr
  OR poserr < maxnegposerr
    ;01019 Fejl: Følgefejl på lift.
    SET errorCode 1019
		SET brakerelease 0
    SET motor 0.0
    SET motorenable 0
    SETSTATE ST_WAIT_CAMGEN_IDLE
  ENDTEST
  TEST powerok = 0
		SET brakerelease 0
    SET motor 0.0
    SET motorenable 0
    SETSTATE ST_WAIT_CAMGEN_IDLE
	ENDTEST
END

State=ST_WAIT_CAMGEN_IDLE
  TEST CamGen = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_ENABLE_WAIT
	CALC encoder = raw - home
  CALC encoder = encoderdir * encoder
  SET motor holdSpeed
  SET motorenable 1
  TEST TimeMotorEnable = 1
	  TIMEOUT TimeBrakeRelease ST_BRAKERELEASE_WAIT
    SETSTATE ST_BRAKERELEASE_WAIT
  ENDTEST
END

State=ST_BRAKERELEASE_WAIT
	CALC encoder = raw - home
  CALC encoder = encoderdir * encoder
	SET brakerelease 1
  TEST TimeBrakeRelease = 1
    ;reset
    SET poserr 0.0
    SET integral 0.0
    TIMEOUT TimeStatic ST_RUN
    SETSTATE ST_RUN    
  ENDTEST
	TEST powerok = 0
		SET brakerelease 0
    SET motorenable 0
	ENDTEST
END

State=ST_STARTHOME
	SET homed 0
	SET homeclicks 0
  SET motorenable 0
	SET motor 0.0
	SET brakerelease 0
	SET encoderreset 2
	TIMEOUT TimeHomeStop ST_HOME
	SETSTATE ST_HOME
END

State=ST_HOME 
	TEST homeclicks >= maxhomeclicks
		SETSTATE ST_ENDHOME
	ENDTEST
  TEST TimeHomeStop = 1
		SET brakerelease 1
		TIMEOUT TimeHomeGo ST_HOME2
		SETSTATE ST_HOME2
  ENDTEST
END

State=ST_HOME2
  TEST TimeHomeGo = 1
		SET brakerelease 0
		CALC homeclicks = homeclicks + 1
		TIMEOUT TimeHomeStop ST_HOME
		SETSTATE ST_HOME
  ENDTEST
END

State=ST_ENDHOME
	SET brakerelease 1
	TIMEOUT TimeHomeEnd ST_ENDHOME2
  SETSTATE ST_ENDHOME2
END

State=ST_ENDHOME2
	SET encoderreset 6
  SET motorenable 1
	SET motor 0.0
  SET brakerelease 1
	TEST TimeHomeEnd = 1
		SET homed 1
	  SET encoderreset 2
	  CALC encoder = raw - home
    CALC encoder = encoderdir * encoder
		TIMEOUT TimeStatic ST_RUN
		SETSTATE ST_RUN
	ENDTEST
END
