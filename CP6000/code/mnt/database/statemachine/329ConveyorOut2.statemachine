STATEMACHINEVERSION 3
Name=ConveyorOut2
Output=motor "ConveyorOut2 Motor"
linkValue=foto test ConveyorOut2Foto1
linkValue=nextFoto test ConveyorOutFoto1Quick
Link=parent Parent
Link=prev KasseLift
Value=errorCode 0
Value=simulate 0
Value=receiving 0
linkValue=nextGetCase getCase ConveyorOut
Timeout=TimeStartup 4000000
Timeout=TimeGet 10000
Timeout=maxReceiveTime 10000000
Timeout=minReceiveTime 5000000

State=ST_HALT
END

State=ST_TIMER
END

State=ST_IDLE
  SET motor 0
	SET receiving 0
  TEST parent = ST_RESET
  OR simulate = 1
		SET errorCode 0
	  SETSTATE ST_RESET
  ENDTEST
END

State=ST_GET
	SET motor 1
	TEST TimeGet = 1
		SETSTATE ST_READY
	ENDTEST
END

State=ST_EMPTY
	SET motor 0
	TEST prev = ST_SEND
		TIMEOUT minReceiveTime ST_RECEIVE
		SETSTATE ST_RECEIVE
	ENDTEST
	TEST simulate = 1
	AND foto = 0
		SETSTATE ST_READY
	ENDTEST
  TEST parent = ST_IDLE
  AND simulate = 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_RECEIVE
	SET motor 1
	TEST foto = 0
		SET receiving 0
		TIMEOUT TimeGet ST_GET
		SETSTATE ST_GET
	ELSE
		TEST receiving = 0
			TEST minReceiveTime = 1
				SET receiving 1
				TIMEOUT maxReceiveTime ST_RECEIVE
				SETSTATE ST_RECEIVE
			ENDTEST
		ELSE
			TEST maxReceiveTime = 1
			OR prev != ST_SEND
				SET receiving 0
				SET errorCode 1003
				SETSTATE ST_IDLE
			ENDTEST
		ENDTEST
	ENDTEST
END

State=ST_READY
  TEST nextGetCase = 1
    SETSTATE ST_SEND
  ELSE
    SET motor 0
  ENDTEST
  TEST parent = ST_IDLE
  AND simulate = 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_SEND
  SET motor 1
  TEST nextGetCase = 0
  OR nextFoto = 1
		TEST foto = 1
			SETSTATE ST_EMPTY
		ELSE
			SETSTATE ST_READY
		ENDTEST
  ENDTEST
END

State=ST_RESET
	TEST foto = 1
		TIMEOUT TimeStartup ST_STARTUP
		SETSTATE ST_STARTUP
	ELSE
		SETSTATE ST_READY
	ENDTEST
END

State=ST_STARTUP
	SET motor 1
	TEST foto = 0
		TIMEOUT TimeGet ST_GET
		SETSTATE ST_GET
	ELSE
		TEST TimeStartup = 1
			SET motor 0
			SETSTATE ST_EMPTY
		ENDTEST
	ENDTEST
END
