STATEMACHINEVERSION 3
Name=LangBufferSkub
;** Input **
Input=klapinde "LangBufferSkub Klap-"
;** Output **
Output=klap "LangBufferSkub Klap"
;** Link **
Link=CamGen CamGenSkub
Link=pstate ProductState
Link=Counter ProductCount
Link=PushPattern
Link=Conveyors ProductConveyors
Link=bplads BufferPlads
Link=parent Parent
;** linkValue **
linkValue=firstRun WorkCell
linkValue=curpm CamGen
linkValue=maxpush PushPattern
linkValue=gettime PushPattern
linkValue=curppp PushPattern
linkValue=timeKlapDown Program
linkValue=bufferstartingup startingup bplads
linkValue=full bplads
linkValue=numofproducts Counter
;** Table **
;** Item **
;** Const **
Const=klapindpm 350
Const=klapudpm 900
;** Value **
Value=numpush 0
Value=remaining 0
Value=startingup 0
Value=forwardstay 0
Value=errorCode 0
;** Timeout **
Timeout=TimeWaitBack 0
Timeout=TimeKlapDown 300000
Timeout=TimeGet 0

State=ST_TIMER
END

State=ST_HALT
END

State=ST_IDLE
  SET klap 0
	TEST parent = ST_RESET
    SET errorCode 0
		SETSTATE ST_RESET
	ENDTEST
END

State=ST_RESET
	SET TimeKlapDown timeKlapDown
	TEST firstRun = 1
		SET forwardstay 0
		SET numpush 0
	ENDTEST
	SET startingup 1
	TEST forwardstay = 0
		SET klap 0
		SETSTATE ST_BEGINBACK
	ELSE
		SET klap 1
		SET numpush 0
		TIMEOUT TimeKlapDown ST_STAY_FINISH
		SETSTATE ST_STAY_FINISH
	ENDTEST
END

State=ST_STARTUP
	SET startingup 0
	TEST Counter = ST_RUN
  OR Counter = ST_RUN_MANUELT
		SETSTATE ST_WAIT
	ENDTEST
	TEST parent = ST_IDLE
		SETSTATE ST_IDLE
	ENDTEST
END

State=ST_WAIT
	SET klap 1
	TEST numofproducts >= curppp
  AND Counter = ST_RUN
  AND PushPattern = ST_GO
    TEST Conveyors = ST_RUN
    OR Conveyors = ST_GOSTOP
      ; start timer
      ; hvis bånd stopper før timer udløber -> for tæt
		  SET TimeGet gettime
      TIMEOUT TimeGet ST_WAIT2
		  SETSTATE ST_WAIT2
    ELSETEST Conveyors = ST_STOP
      SETSTATE ST_WAIT3
    ENDTEST
	ENDTEST
	TEST parent = ST_IDLE
		SETSTATE ST_IDLE
	ENDTEST
END

State=ST_WAIT2
  TEST TimeGet = 1
		TEST bplads = ST_WAITPUSH
  	OR bufferstartingup = 0
	  AND bplads = ST_GOCLOSE
		  CALC remaining = maxpush - numpush
			TEST remaining <= 1
				SETSTATE ST_BEGINFORWARDSTAY
  		ELSE
	  		SETSTATE ST_BEGINFORWARD
		  ENDTEST
		ENDTEST
  ELSETEST Conveyors = ST_STOP
    SET errorCode 1011
    SETSTATE ST_IDLE
  ENDTEST
	TEST parent = ST_IDLE
		SETSTATE ST_IDLE
	ENDTEST
END

State=ST_WAIT3
	TEST bplads = ST_WAITPUSH
  OR bufferstartingup = 0
	AND bplads = ST_GOCLOSE
		CALC remaining = maxpush - numpush
		TEST remaining <= 1
			SETSTATE ST_BEGINFORWARDSTAY
  	ELSE
	  	SETSTATE ST_BEGINFORWARD
		ENDTEST
	ENDTEST
	TEST parent = ST_IDLE
		SETSTATE ST_IDLE
	ENDTEST
END

State=ST_BEGINFORWARD
  TEST CamGen = ST_START
		CALC numpush = numpush + 1
    SETSTATE ST_FORWARD
  ENDTEST
	TEST parent = ST_IDLE
		SETSTATE ST_IDLE
	ENDTEST
END

State=ST_BEGINFORWARDSTAY
  TEST CamGen = ST_START
		SET numpush 0
    SETSTATE ST_FORWARDSTAY
  ENDTEST
	TEST parent = ST_IDLE
		SETSTATE ST_IDLE
	ENDTEST
END

State=ST_FORWARD
	TEST curpm > klapindpm
	AND curpm < klapudpm
		SET klap 0
	ELSE
		SET klap 1
	ENDTEST
	TEST CamGen = ST_STOP
		SETSTATE ST_WAIT
	ENDTEST
	TEST CamGen = ST_NOCHOOSETRACK
  OR CamGen = ST_IDLE
		SETSTATE ST_IDLE
	ENDTEST
END

State=ST_FORWARDSTAY
	SET klap 1
	TEST CamGen = ST_STOP
		TIMEOUT TimeKlapDown ST_STAY_FINISH
		SETSTATE ST_STAY_FINISH
	ENDTEST
	TEST CamGen = ST_NOCHOOSETRACK
  OR CamGen = ST_IDLE
		SETSTATE ST_IDLE
	ENDTEST
END

;TimeKlapDown should be < time to open shutters of BufferPlads (which is roughly 350000 us)

State=ST_STAY_FINISH
	SET forwardstay 1
	SET numpush 0
	TEST bplads = ST_OPEN
	OR TimeKlapDown = 1
		SET forwardstay 0
		SET klap 0
		TIMEOUT TimeWaitBack ST_BEGINBACK
		SETSTATE ST_TIMER
	ENDTEST
	TEST parent = ST_IDLE
		SETSTATE ST_IDLE
	ENDTEST
END

State=ST_BEGINBACK
  SET klap 0
  TEST CamGen = ST_START
    SETSTATE ST_GOBACK
  ENDTEST
	TEST parent = ST_IDLE
		SETSTATE ST_IDLE
	ENDTEST
END

State=ST_GOBACK
	SET klap 0
	TEST startingup = 0
	AND curpm > klapudpm
		SET klap 1
	ENDTEST
  TEST CamGen = ST_STOP
  	TEST startingup = 1
			SETSTATE ST_STARTUP
		ELSE
			SETSTATE ST_WAIT
		ENDTEST
  ENDTEST
	TEST CamGen = ST_NOCHOOSETRACK
  OR CamGen = ST_IDLE
		SETSTATE ST_IDLE
	ENDTEST
END
