STATEMACHINEVERSION 3
Name=ServoCardBoard
Link=Master CardBoardShuttle
Link=parent Parent
Output=home_vel "CardBoardShuttle Homevel"
Output=home_tq "CardBoardShuttle Hometorque"
Output=Mode "CardBoardShuttle Mode"
Output=setpoint "CardBoardShuttle MacSetpoint"
Output=vsoll "CardBoardShuttle Maxvel"
Output=asoll "CardBoardShuttle Maxacc"
Output=set_curpos "CardBoardShuttle Curpos"
Output=kvout "CardBoardShuttle Load Factor"
Output=set_status "CardBoardShuttle Status"
Input=temperature "CardBoardShuttle Temperature"
Input=Inpos "CardBoardShuttle IN_POS"
Input=curpos "CardBoardShuttle Curpos"
Input=curtorque "CardBoardShuttle Curtorque"
Input=homedone "CardBoardShuttle HOME_DONE"
Input=status "CardBoardShuttle Status"
Input=ShuttleUp "CardBoardShuttle ReadUp"
Input=ShuttleCrash "CardBoardShuttle Crash"
Value=errorCode 0
Value=simulate 0
Value=curpm 0.0
Value=homed 0
Value=temp 0
Const=statusOffset 0
Const=loadfactor 3.0000
Const=home_speed -250.0000
Const=home_torque -50.0000
Const=acceleration 15000.0000
Const=cruise_speed 2700.0000
Const=testmode 0
Value=teststate 0
Value=distance 0.0
Value=startpos 0.0
Value=goIdle 0
Value=moveIsSafe 0
Const=overrideSafe 0
; tickspermotorround*gearudveksling/(eff_rad*2*pi)
; eks: 8000*13.3/(27.5*2*pi)
Const=tickspermm 615.7849434537
; All posXX in mm.
Const=posHome 10.0
Const=posPick 1578.0000
Const=posPlace 378.0000
Const=posTestBegin 30.0000
Const=posTestEnd 230.0000
Timeout=TimeTest 5000000
Timeout=TimeCmdDelay 500000
Timeout=TimeStartMax 1000000
Timeout=TimeHome 10000000

State=ST_TIMER
END

State=ST_HALT
END

State=ST_IDLE
	TEST overrideSafe = 0
		SET moveIsSafe 0
	ENDTEST
	TEST parent = ST_RESET
		SET errorCode 0
    CALC temp = status + statusOffset
		TEST temp != 16
	  AND temp != 32
	  AND temp != 48
	  AND temp != 64
	  AND temp != 80
	  AND temp != 0
      SET set_status 0
		ENDTEST
		SETSTATE ST_RESET
	ENDTEST
END

State=ST_RESET
	SET errorCode 0
	TEST parent = ST_IDLE
	AND simulate = 0
		SETSTATE ST_IDLE
	ELSETEST moveIsSafe = 1
	AND ShuttleUp = 1
		SET kvout loadfactor
		SET asoll acceleration
		TEST homed = 0
			SET home_vel home_speed
			SET home_tq home_torque
			SET temp 0
			TIMEOUT TimeCmdDelay ST_HOME
			SETSTATE ST_HOME
		ELSE
			SET vsoll cruise_speed
			SET setpoint curpos
			TIMEOUT TimeCmdDelay ST_INIT
			SETSTATE ST_INIT
		ENDTEST
	ENDTEST
END

State=ST_HOME
	TEST temp = 0
	AND TimeCmdDelay = 1
	AND ShuttleUp = 1
		SET Mode 12
		SET temp 1
		TIMEOUT TimeHome ST_HOME
		SETSTATE ST_HOME
	ELSETEST temp = 1
		TEST homedone = 1
			SET homed 1
			SET Mode 0
			SET vsoll cruise_speed
			TIMEOUT TimeCmdDelay ST_HOME2
			SETSTATE ST_HOME2
		ELSETEST TimeHome = 1
		AND simulate = 0
			SET errorCode 1057
			SET Mode 0
			SETSTATE ST_IDLE
		ENDTEST
	ENDTEST
END

State=ST_HOME2
	TEST TimeCmdDelay = 1
		SET setpoint curpos
		TIMEOUT TimeCmdDelay ST_HOME3
		SETSTATE ST_HOME3
	ENDTEST
END

State=ST_HOME3
	TEST TimeCmdDelay = 1
		SET Mode 2
		TIMEOUT TimeCmdDelay ST_HOME4
		SETSTATE ST_HOME4
	ENDTEST
END

State=ST_HOME4
	TEST TimeCmdDelay = 1
		CALC setpoint = tickspermm * posHome
		SET startpos curpos
		CALC distance = setpoint - curpos
		TIMEOUT TimeStartMax ST_START
		SETSTATE ST_START
	ENDTEST
END

State=ST_INIT
	TEST TimeCmdDelay = 1
		SET Mode 2
		SETSTATE ST_CHOOSETRACK
	ENDTEST
END

State=ST_CHOOSETRACK
	TEST Master = ST_IDLE
	AND testmode = 0
		SET goIdle 1
		CALC setpoint = tickspermm * posHome
		SET startpos curpos
		CALC distance = setpoint - curpos
		TIMEOUT TimeStartMax ST_START
		SETSTATE ST_START
	ELSETEST Master = ST_BEGINHOME
	AND ShuttleUp = 1
		CALC setpoint = tickspermm * posHome
		SET startpos curpos
		CALC distance = setpoint - curpos
		TIMEOUT TimeStartMax ST_START
		SETSTATE ST_START
	ELSETEST Master = ST_BEGINPICK
	AND ShuttleUp = 1
		CALC setpoint = tickspermm * posPick
		SET startpos curpos
		CALC distance = setpoint - curpos
		TIMEOUT TimeStartMax ST_START
		SETSTATE ST_START
	ELSETEST Master = ST_BEGINPLACE
	AND ShuttleUp = 1
		CALC setpoint = tickspermm * posPlace
		SET startpos curpos
		CALC distance = setpoint - curpos
		TIMEOUT TimeStartMax ST_START
		SETSTATE ST_START
	ELSETEST testmode = 1
		TIMEOUT TimeTest ST_TEST
		SETSTATE ST_TEST
	ENDTEST
END

State=ST_TEST
	TEST TimeTest = 1
 		TEST teststate = 0
			SET teststate 1
			CALC setpoint = tickspermm * posTestBegin
			SET startpos curpos
			CALC distance = setpoint - curpos
			TIMEOUT TimeStartMax ST_START
			SETSTATE ST_START
		ELSETEST teststate = 1
			SET teststate 0
			CALC setpoint = tickspermm * posTestEnd
			SET startpos curpos
			CALC distance = setpoint - curpos
			TIMEOUT TimeStartMax ST_START
			SETSTATE ST_START
		ENDTEST
	ENDTEST
END

State=ST_STOP
	SET curpm 1000.0
	TEST Master = ST_IDLE
	AND goIdle = 1
    SET goIdle 0
		SET Mode 0
		SETSTATE ST_IDLE
  ELSE		
		SETSTATE ST_CHOOSETRACK
	ENDTEST
END

State=ST_START
	SET curpm 0.0
	TEST Inpos = 0
	OR TimeStartMax = 1
	  SETSTATE ST_RUN
  ENDTEST
END

State=ST_RUN
	CALC curpm = curpos - startpos
	CALC curpm = curpm / distance
	CALC curpm = curpm * 1000.0
	TEST Inpos = 1
    SETSTATE ST_STOP
  ENDTEST
  CALC temp = status + statusOffset
	TEST temp != 16
	AND temp != 32
	AND temp != 48
	AND temp != 64
	AND temp != 80
	AND temp != 0
    SET Mode 0
    ;SET homed 0
    TIMEOUT TimeCmdDelay ST_ERROR
    SETSTATE ST_TIMER
  ENDTEST
  TEST ShuttleCrash = 0
    SET errorCode 4202
    SET Mode 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_ERROR
  CALC temp = status + statusOffset
	TEST temp != 16
	AND temp != 32
	AND temp != 48
	AND temp != 64
	AND temp != 80
	AND temp != 0
    SET set_status 0
  ENDTEST
  SETSTATE ST_IDLE
END
