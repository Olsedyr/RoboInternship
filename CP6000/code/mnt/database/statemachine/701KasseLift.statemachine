STATEMACHINEVERSION 3
Name=KasseLift
Output=Centrer "KasseCentrer"
Output=Motor "KasseLift Conveyor Motor" 
Link=CamGen ServoLift
Link=parent Parent
Link=KasseInLiner
Link=BufferPlads
Link=ConveyorOutCP
Link=Foto KasseLiftFoto
linkValue=gostoppos ServoStop 
linkValue=firstRun WorkCell
linkValue=Fototest test Foto
linkValue=maxlayers Program
linkValue=timeMotorInGoDown Program
linkValue=noShakeOnLast Program
linkValue=waitInR3 Program
linkValue=full BufferPlads
Value=errorCode 0
Value=layers 0
Value=bufferlayers 0
Value=curshake 0
Value=reboot 0
Value=startingup 0
Value=inShake 0
Value=temp 0.0
Const=godownpct 0.7
Timeout=KStopDelay 2000000
Timeout=TimeStartUp 4000000
Timeout=TimeWaitShake 300000
Timeout=TimeSend 10000000
Timeout=TimePapMotor 0
Timeout=TimeW3 3000000

; reboot = 0 -> ren rÃ¸v
; reboot = 1 -> starter med tom kasse
; reboot = 2 -> starter med fyldt kasse

State=ST_TIMER
END

State=ST_HALT
END

State=ST_IDLE
	SET Centrer 0
	SET gostoppos 0
	SET Motor 0
	TEST parent = ST_RESET
		SETSTATE ST_RESET
	ENDTEST
END

State=ST_RESET
	SET errorCode 0
	TEST firstRun = 1
		SET reboot 0
		SET layers 0
		SET bufferlayers 0
	ENDTEST
	SET curshake 0
	SET inShake 0
	SET startingup 1
	SET Motor 0
	TEST CamGen = ST_CHOOSETRACK
		SETSTATE ST_BEGINHOME
	ENDTEST
END

State=ST_STARTUP
	SET startingup 0
	TEST Foto = ST_ON
		TEST reboot = 1
			SETSTATE ST_READY1
		ELSE
			TEST reboot = 2
				TIMEOUT TimeW3 ST_READY3
				SETSTATE ST_READY3
			ELSE
				TEST reboot = 0
					TIMEOUT KStopDelay ST_PREPARE_SEARCHBOX
					SETSTATE ST_PREPARE_SEARCHBOX
				ENDTEST
			ENDTEST
		ENDTEST
	ELSE
		TEST Foto = ST_OFF
			TEST reboot = 0
				SETSTATE ST_EMPTY
			ELSE
				TIMEOUT KStopDelay ST_PREPARE_SEARCHBOX
				SETSTATE ST_PREPARE_SEARCHBOX
			ENDTEST
	  ENDTEST
	ENDTEST
	TEST parent = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_PREPARE_SEARCHBOX
	SET Centrer 0
	SET gostoppos 1
	SET Motor 1
	TEST KStopDelay = 1
		TIMEOUT TimeStartUp ST_SEARCHBOX
		SETSTATE ST_SEARCHBOX
  ENDTEST
END

State=ST_SEARCHBOX
	SET Motor 1
	TEST TimeStartUp = 1
		SET reboot 0
		SETSTATE ST_STARTUP
  ENDTEST
	TEST Fototest = 1
		SET reboot 1
		SETSTATE ST_STARTUP
	ENDTEST
	TEST parent = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_EMPTY
	SET gostoppos 1
	SET Centrer 0
  TEST KasseInLiner = ST_SEND
		SET Motor 1
		SETSTATE ST_RECEIVE
	ELSE
		SET Motor 0
	ENDTEST
	TEST parent = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_RECEIVE
	SET Motor 1
	TEST Fototest = 1
	AND ConveyorOutCP != ST_RECEIVE
		SET reboot 1
		SETSTATE ST_READY1
	ENDTEST
	TEST KasseInLiner = ST_IDLE
		SETSTATE ST_IDLE
	ENDTEST
END

State=ST_READY1
	SET Motor 0
	SET Centrer 1
	TEST CamGen = ST_CHOOSETRACK
		SETSTATE ST_BEGINHIGH
	ENDTEST
	TEST parent = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_READY2
	SET gostoppos 0
	TEST full = 1
		CALC bufferlayers = layers + 1
		TEST bufferlayers >= maxlayers
			SET bufferlayers 0
		ENDTEST
	ENDTEST
	TEST BufferPlads = ST_OPEN
		CALC layers = layers + 1
		TEST layers >= maxlayers
			SET layers 0
		ENDTEST
		SET bufferlayers layers
		SETSTATE ST_SHAKE1
	ENDTEST
	TEST parent = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_SHAKE1
	TEST layers = 0
	AND noShakeOnLast = 1
		SETSTATE ST_SHAKEDONE
	ELSE
		TEST CamGen = ST_CHOOSETRACK
			SET inShake 1
			SETSTATE ST_BEGINHIGH
		ENDTEST
	ENDTEST
END

State=ST_SHAKE2
	TEST BufferPlads = ST_EMPTIED
		TIMEOUT TimeWaitShake ST_SHAKE3
		SETSTATE ST_SHAKE3
	ENDTEST
END

State=ST_SHAKE3
	TEST TimeWaitShake = 1
		SET inShake 2
		TEST CamGen = ST_CHOOSETRACK
			SETSTATE ST_BEGINHIGH
		ENDTEST
	ENDTEST
END

State=ST_SHAKEDONE
	SET inShake 0
	TEST layers = 0
		SET reboot 2
		SETSTATE ST_GODOWN
	ELSE
		SETSTATE ST_READY2
  ENDTEST
END

State=ST_GODOWN
	TEST timeMotorInGoDown != 0
		SET Centrer 0
	ENDTEST
	TEST CamGen = ST_CHOOSETRACK
		SET TimePapMotor timeMotorInGoDown
		TIMEOUT TimePapMotor ST_BEGINHOME
		SETSTATE ST_BEGINHOME
	ENDTEST
END

State=ST_READY3
	SET gostoppos 0
	SET Centrer 0
	TEST full = 1
		CALC bufferlayers = layers + 1
		TEST bufferlayers >= maxlayers
			SET bufferlayers 0
		ENDTEST
	ENDTEST
	TEST waitInR3 = 0
	OR TimeW3 = 1
		TEST ConveyorOutCP = ST_EMPTY
			SETSTATE ST_SEND
		ENDTEST
	ENDTEST
	TEST parent = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_SEND
	TEST ConveyorOutCP = ST_PRECEIVE
	OR ConveyorOutCP = ST_RECEIVE
		SET Motor 1
		TIMEOUT TimeSend ST_SEND2
		SETSTATE ST_SEND2
	ENDTEST
END

State=ST_SEND2
	TEST ConveyorOutCP != ST_PRECEIVE
	AND Fototest = 0
		SET reboot 0
		SETSTATE ST_EMPTY
	ENDTEST
	TEST timeMotorInGoDown != 0
	AND Fototest = 0
		SET gostoppos 1
	ENDTEST
	TEST TimeSend = 1
		SET errorCode 1014
		SETSTATE ST_IDLE
	ENDTEST
END

State=ST_BEGINHOME
  TEST CamGen = ST_START
    SETSTATE ST_GO
  ENDTEST
	TEST parent = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_BEGINHIGH
  TEST CamGen = ST_START
    SETSTATE ST_GO
  ENDTEST
	TEST parent = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_GO
	TEST reboot = 2
		CALC temp = timeMotorInGoDown * godownpct
		TEST TimePapMotor < temp
			SET Motor 1
		ENDTEST
		TEST TimePapMotor = 1
			SET Motor 0
		ENDTEST
	ENDTEST
  TEST CamGen = ST_STOP
		TEST startingup = 1
			SETSTATE ST_STARTUP
		ELSE
			TEST inShake = 1
				SETSTATE ST_SHAKE2
			ELSE
				TEST inShake = 2
					SETSTATE ST_SHAKEDONE
				ELSE
					TEST reboot = 1
						SETSTATE ST_READY2
					ELSE
						TEST reboot = 2
							SET Motor 0
							TIMEOUT TimeW3 ST_READY3
							SETSTATE ST_READY3
						ENDTEST
					ENDTEST
				ENDTEST
			ENDTEST
		ENDTEST
	ENDTEST
END
