STATEMACHINEVERSION 3
Name=Pallebane8
;Input=palletInPlace "Pallebane8 Foto"
Output=Motor "Pallebane8 Motor"
Link=next Pallestabler
Link=prev Strapper
Link=parent Parent 
Timeout=maxDelayStop 10000000
Timeout=StartupTimeout 5000000
Timeout=StopDelay 0
Timeout=PickupDelay 0
Timeout=ReadyDelay 0
Const=autoDetectPallet 1
Const=ON 1
linkValue=palletStackableStrapper Strapper
linkValue=palletInPlace test PB8Foto
Value=palletStackable8 0
Value=simulate 0
Value=simAllStackable 0
;en pallebane går automatisk fra ready til empty hvis fotocelle slukker
;en pallebane går automatisk fra empty til ready hvis fotocelle tænder


State=ST_HALT
END

State=ST_TIMER
END

State=ST_IDLE
  SET Motor 0
  TEST parent = ST_RESET
  OR parent = ST_RUN
  OR simulate > 0
    TEST simAllStackable = 1
      SET palletStackable8 1
    ENDTEST
    SETSTATE ST_RESET
  ENDTEST
END

State=ST_READY
  TEST simAllStackable = 1
  	SET palletStackable8 1
  ENDTEST
  TEST palletInPlace != ON 
  AND autoDetectPallet = 1
    TIMEOUT PickupDelay ST_EMPTY
    SETSTATE ST_TIMER
  ELSE
    TEST next = ST_EMPTY
      TIMEOUT maxDelayStop ST_SEND
      SETSTATE ST_SEND
    ELSE 
      SET Motor 0
    ENDTEST
  ENDTEST
  TEST prev = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_SEND
  TEST next = ST_READY
    SETSTATE ST_EMPTY
  ELSE
    TEST maxDelayStop = 1
      SETSTATE ST_RESET
    ELSE
      TEST next = ST_RECEIVE
        SET Motor 1
      ENDTEST
    ENDTEST
  ENDTEST
END

State=ST_EMPTY
  TEST palletInPlace = ON 
  AND autoDetectPallet = 1
    TIMEOUT ReadyDelay ST_READY
    SETSTATE ST_TIMER
  ELSE
    TEST prev = ST_SEND
      TIMEOUT maxDelayStop ST_RECEIVE
      SETSTATE ST_RECEIVE
    ELSE 
			TEST prev = ST_RECEIVE
				SET Motor 1
			ELSE
				SET Motor 0
			ENDTEST
    ENDTEST
  ENDTEST
  TEST prev = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_RECEIVE
  TEST palletInPlace = ON
    TEST simAllStackable = 0
		  SET palletStackable8 palletStackableStrapper
		  SET palletStackableStrapper 0
    ELSE
  		SET palletStackable8 1
    ENDTEST
    TIMEOUT StopDelay ST_READY
    SETSTATE ST_TIMER
  ELSE
    TEST maxDelayStop = 1
      SETSTATE ST_RESET
    ELSE
      TEST prev = ST_SEND
        SET Motor 1
      ENDTEST
    ENDTEST
  ENDTEST
END

State=ST_RESET
  TEST palletInPlace = ON
    SETSTATE ST_READY
  ELSE
    TIMEOUT StartupTimeout ST_STOP
    SETSTATE ST_STOP
  ENDTEST
END

State=ST_STOP
  TEST palletInPlace = ON
    SETSTATE ST_READY
  ELSE
    TEST StartupTimeout = 1
      SETSTATE ST_EMPTY
    ELSE
			TEST StartupTimeout < 2000000
			AND prev = ST_EMPTY
				SET Motor 1
			ENDTEST
		ENDTEST
	ENDTEST
END
