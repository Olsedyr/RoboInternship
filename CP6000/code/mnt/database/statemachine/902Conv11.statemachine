STATEMACHINEVERSION 3
Name=Conv11
linkValue=fototest test Conv11Foto1
Output=Motor "Conv11 Motor"
Link=prev Conv10
Link=next KasseInLiner
Link=parent Parent
linkValue=pulling KasseInLiner
;Time for case/product to leave conveyor after foto has gone free
Timeout=freeWaitTime 1000000
linkValue=firstRun WorkCell
Timeout=startupTime 5000000
Timeout=maxReceiveTime 10000000
Timeout=maxSendTime 10000000
Timeout=StopDelay 0
Timeout=EmptyDelay 0
Timeout=ReadyDelay 0
Timeout=TimeWaitPushBack 2400000
Timeout=timeBeforeReset 2000000
Const=autoDetectItem 1
Const=lengthMM 4000.0
Const=speedMS 0.6
Value=simulate 0
Value=pullseen 0
Value=errorCode 0
Value=timelength 0.0


;en transportør går automatisk fra ready til empty hvis fotocelle slukker
;og fra empty til ready hvis fotocelle tænder

State=ST_TIMER
END

State=ST_HALT
END

State=ST_IDLE
  SET Motor 0
	TEST parent = ST_RESET
  OR simulate = 1
    SET errorCode 0
		SETSTATE ST_RESET
	ENDTEST
END

State=ST_READY
  TEST fototest = 0 
  AND autoDetectItem = 1
    TIMEOUT EmptyDelay ST_EMPTY
    SETSTATE ST_TIMER
  ELSE
    TEST next = ST_EMPTY
			SET pullseen 0
      TIMEOUT maxSendTime ST_SEND
      SETSTATE ST_SEND
    ELSE 
      SET Motor 0
    ENDTEST
  ENDTEST
  TEST parent = ST_IDLE
  AND simulate = 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_SEND
	SET Motor 0
	TEST pulling = 1
		SET pullseen 1
	ENDTEST
	TEST pullseen = 1
	AND pulling = 0
		SET pullseen 0
		TIMEOUT TimeWaitPushBack ST_WAITBACK
    SETSTATE ST_WAITBACK
  ELSE
    TEST maxSendTime = 1
			SET pullseen 0
      TIMEOUT timeBeforeReset ST_TIMER
      SETSTATE ST_RESET
	  ENDTEST
  ENDTEST
END

State=ST_WAITBACK
	TEST TimeWaitPushBack = 1
		SETSTATE ST_EMPTY
	ENDTEST
END

State=ST_EMPTY
  TEST fototest = 1
  AND autoDetectItem = 1
    TIMEOUT ReadyDelay ST_READY
    SETSTATE ST_TIMER
  ELSE
    TEST prev = ST_SEND
      TIMEOUT maxReceiveTime ST_RECEIVE
      SETSTATE ST_RECEIVE
    ELSE 
      SET Motor 0
    ENDTEST
  ENDTEST
  TEST parent = ST_IDLE
  AND simulate = 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_RECEIVE
  TEST fototest = 1
    TIMEOUT StopDelay ST_READY
    SETSTATE ST_TIMER
  ELSE
    TEST maxReceiveTime = 1
      SETSTATE ST_RESET
    ELSE
      TEST prev = ST_SEND
        SET Motor 1
      ENDTEST
    ENDTEST
  ENDTEST
END

State=ST_RESET
  TEST fototest = 1
		SETSTATE ST_READY
	ELSE
    CALC timelength = lengthMM / speedMS 
    ;*1500 = kør bånd 1.5 gang igennem for at finde produkt
    CALC timelength = timelength * 1500
    SET startupTime timelength
    TIMEOUT startupTime ST_STARTUP
    SETSTATE ST_STARTUP
	ENDTEST
END


State=ST_STARTUP
  TEST fototest = 1 
    SETSTATE ST_READY
  ELSE
    TEST startupTime = 1
      SETSTATE ST_EMPTY
    ELSE
      SET Motor 1
    ENDTEST
  ENDTEST
END
