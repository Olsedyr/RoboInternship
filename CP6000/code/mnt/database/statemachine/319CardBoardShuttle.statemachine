STATEMACHINEVERSION 3
Name=CardBoardShuttle
Output=Lower "CardBoardShuttle Lower"
Output=Suck "CardBoardShuttle Suck"
Input=Photo "CardBoardShuttle Photo"
Input=ShuttleUp "CardBoardShuttle ReadUp"
Input=ShuttleDown "CardBoardShuttle ReadDown"
Link=scissorpick ScissorDepalletizer2_3
Link=convplace CardBoardConveyor
linkValue=inTopPos ScissorDepalletizer2_3
linkValue=upstep scissorpick
linkValue=downstep newsheet convplace
linkValue=finito scissorpick
linkValue=moveIsSafeForDePal moveIsSafe ScissorDepalletizer2_3
Link=Servo ServoCardBoard
Link=parent Parent
Value=errorCode 0
Value=nextcardboardlayer 0
Value=tries 0
Value=clearNext 0
Value=suckTest 0
Const=noSuckNoPhoto 0
Const=maxtries 3
Timeout=TimePickDown 4000000
Timeout=TimePickUp 4000000
Timeout=TimePlaceDown 3000000
Timeout=TimePlaceUp 3000000
Timeout=TimePlace 500000

State=ST_TIMER
END

State=ST_HALT
END

State=ST_IDLE
	SET Lower 0
	SET Suck 0
	SET moveIsSafeForDePal 0
	TEST parent = ST_RESET
		SET errorCode 0
    SETSTATE ST_RESET
  ENDTEST
END

State=ST_RESET
	SET errorCode 0
  TEST suckTest = 1
    TIMEOUT suckTestTime ST_SUCKTEST
    SETSTATE ST_SUCKTEST
  ELSETEST parent = ST_IDLE
		SETSTATE ST_IDLE
	ELSETEST Servo = ST_CHOOSETRACK
		SET clearNext 0
		SETSTATE ST_BEGINHOME
	ENDTEST
END

Timeout=suckTestTime 3000000
Value=suckDown 0

State=ST_SUCKTEST
  TEST suckTestTime = 1
    TEST suckDown = 0
      SET Lower 1
      SET Suck 1
      SET suckDown 1
      ;TIMEOUT suckTestTime ST_SUCKTEST
    ELSETEST suckDown = 1
    AND ShuttleDown = 1
      SET Lower 0
      SET suckDown 2
    ELSETEST suckDown = 2
    AND ShuttleUp = 1
      SET Suck 0
      SET suckDown 0
      TIMEOUT suckTestTime ST_SUCKTEST
    ENDTEST
  ENDTEST
END

State=ST_HOME
	SET moveIsSafeForDePal 1
	TEST nextcardboardlayer = 1
	AND scissorpick = ST_READY_ROBOT
	AND upstep = 0
		SETSTATE ST_BEGINDISCARD
	ENDTEST
	TEST parent = ST_IDLE
		SETSTATE ST_IDLE
	ENDTEST
END

State=ST_BEGINDISCARD
  TEST Servo = ST_START
    SETSTATE ST_GODISCARD
  ELSETEST parent = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_GODISCARD
  TEST Servo = ST_STOP
		SETSTATE ST_BEGINPICK 
  ELSETEST Servo = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_BEGINHOME
  TEST Servo = ST_START
    SETSTATE ST_GOHOME
	ENDTEST
END

State=ST_GOHOME
  TEST Servo = ST_STOP
		TEST clearNext = 1
			SET clearNext 0
			SET nextcardboardlayer 0
		ENDTEST
		SETSTATE ST_HOME
  ELSETEST Servo = ST_IDLE
    SETSTATE ST_IDLE
	ENDTEST
END

State=ST_BEGINPICK
  TEST Servo = ST_START
    SETSTATE ST_GOPICK
  ENDTEST
END

State=ST_GOPICK
  TEST Servo = ST_STOP
		SET Lower 1
    TEST noSuckNoPhoto = 0
		  SET Suck 1
    ENDTEST
		SET tries 0
		TIMEOUT TimePickDown ST_PICKDOWN
		SETSTATE ST_PICKDOWN
  ELSETEST Servo = ST_IDLE
    SETSTATE ST_IDLE
	ENDTEST
END

State=ST_PICKDOWN
	TEST TimePickDown = 1
  OR ShuttleDown = 1
		SET Lower 0
    TEST noSuckNoPhoto = 0
		  SET Suck 1
    ENDTEST
		TIMEOUT TimePickUp ST_PICKUP
		SETSTATE ST_PICKUP
	ENDTEST
END

State=ST_PICKUP
  TEST TimePickUp = 1
	OR ShuttleUp = 1
		TEST Photo = 1
    OR noSuckNoPhoto = 1
			SETSTATE ST_BEGINPLACE
		ELSE
			CALC tries = tries + 1
			TEST tries >= maxtries
				TEST inTopPos = 1
					SET finito 1
					SET Lower 0
					SET Suck 0
					SET clearNext 1
					SETSTATE ST_BEGINHOME
				ELSE
					SET errorCode 4201
					SETSTATE ST_IDLE
				ENDTEST
			ELSE
				SET Lower 1
        TEST noSuckNoPhoto = 0
		      SET Suck 1
        ENDTEST
				TIMEOUT TimePickDown ST_PICKDOWN
				SETSTATE ST_PICKDOWN
			ENDTEST
		ENDTEST
	ENDTEST
END

State=ST_BEGINPLACE
  TEST Servo = ST_START
    SETSTATE ST_GOPLACE
  ENDTEST
  TEST Photo = 0
    SET errorCode 4201
		SETSTATE ST_IDLE
  ENDTEST
END

State=ST_GOPLACE
  TEST Servo = ST_STOP
		SET Lower 1
		TEST inTopPos = 1
			SET finito 1
		ELSE			
			SET upstep 1
		ENDTEST
		TIMEOUT TimePlaceDown ST_PLACEDOWN
		SETSTATE ST_PLACEDOWN
  ELSETEST Servo = ST_IDLE
    SETSTATE ST_IDLE
	ENDTEST
  TEST Photo = 0
    SET errorCode 4201
		SETSTATE ST_IDLE
  ENDTEST
END

State=ST_PLACEDOWN
	TEST TimePlaceDown = 1
  OR ShuttleDown = 1
		SET Suck 0
		TIMEOUT TimePlace ST_PLACE
		SETSTATE ST_PLACE
	ENDTEST
  TEST Photo = 0
    SET errorCode 4201
		SETSTATE ST_IDLE
  ENDTEST
END

State=ST_PLACE
	TEST TimePlace = 1
		SET Suck 0
		SET Lower 0
		TIMEOUT TimePlaceUp ST_PLACEUP
		SETSTATE ST_PLACEUP
	ENDTEST
END

State=ST_PLACEUP
	TEST TimePlaceUp = 1
  OR ShuttleUp = 1
		SET downstep 1
		SET clearNext 1
		SETSTATE ST_BEGINHOME
	ENDTEST
END
