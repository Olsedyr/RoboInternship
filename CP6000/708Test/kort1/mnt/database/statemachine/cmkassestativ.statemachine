STATEMACHINEVERSION 3
Include=casekind.statemachine
Name=KasseStativ
;** Input **
;** Output **
Output=hold "KasseStativ Stabel Hold Ventil"
Output=holdud "KasseStativ Stabel Hold Ventil Ud"
Output=freeNetto "KasseStativ Pull Free Netto"
Output=freeOthers "KasseStativ Pull Free Others"
Output=conveyor "KasseStativConveyor Setpoint"
Output=conveyorenable "KasseLiftConveyor Motor Enable"
;** Link **
Link=PalleStation
Link=next PindeTjekker600
Link=modtaget KasseStativFotoModtaget
Link=tom KasseStativFotoTom
Link=afleveret KasseStativFotoAfleveret
Link=conveyorstation ConveyorStation
Link=CamGen CamGenLift
Link=parent Parent
;** linkValue **
;goHome is set from formCMUser.c
linkValue=goHome PalleStation
linkValue=casekind Program8XX
linkValue=stopFree CamGenLift
;** Table **
;** Item **
;** Const **
Const=receiveSpeed 20.0000
Const=sendSpeed 40.0000
;** Value **
Value=errorCode 0
Value=goingHome 0
Value=testmode 0
Value=waitSendAfterIdle 0
;goFree is set from formCMUser.c
Value=goFree 0
;** Timeout **
Timeout=maxReceiveTime 10000000
Timeout=maxSendTime 10000000
Timeout=ventilTime 500000
Timeout=waitStopConveyorTime 150000
Timeout=enableConveyorTime 500000
Timeout=timeWaitHold 500000
Timeout=motorFirstEnableTime 1000000

State=ST_TIMER
END

State=ST_HALT
END

State=ST_IDLE
  SET conveyor 0.0
  SET conveyorenable 0
  TEST goFree = 1
    SET hold 0
    SET holdud 1
    SET freeNetto 0
    SET freeOthers 0
    SET waitSendAfterIdle 0
  ENDTEST
  TEST parent = ST_RESET
    SET goFree 0
    SET errorCode 0
    SET conveyorenable 1
    TIMEOUT motorFirstEnableTime ST_RESET
    SETSTATE ST_TIMER
  ENDTEST
END

State=ST_RESET
  SET conveyorenable 0
  SETSTATE ST_STARTUP
END

State=ST_STARTUP
  TEST CamGen = ST_CHOOSETRACK
    TEST tom = ST_FREE
      SET hold 0
      SET holdud 1
      TEST modtaget = ST_FREE
        ;notifies CamGen to go to receive position
        SETSTATE ST_WAIT_RECEIVE_POS
      ELSE
        TEST modtaget = ST_BLOCKED
          SETSTATE ST_WAIT_HOLD_POS
        ENDTEST
      ENDTEST
    ELSE
      TEST tom = ST_BLOCKED
        TEST modtaget = ST_FREE
          SETSTATE ST_WAIT_RELEASE_POS
        ELSE
          TEST modtaget = ST_BLOCKED
            TEST waitSendAfterIdle = 1
              SET waitSendAfterIdle 0
              SETSTATE ST_WAIT_SEND_POS
            ELSE
              SETSTATE ST_WAIT_HOLD_POS
            ENDTEST
          ENDTEST
        ENDTEST
      ENDTEST
    ENDTEST
  ENDTEST
  TEST afleveret = ST_BLOCKED
    SET errorCode 4122
    SET waitSendAfterIdle 0
    SETSTATE ST_IDLE
  ENDTEST
  TEST parent != ST_RUN
  AND testmode = 0
    SET waitSendAfterIdle 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_WAIT_RECEIVE_POS
  ;wait for CamGen
  TEST CamGen = ST_STOP
    TEST goingHome = 1
      SETSTATE ST_HOME
    ELSE
      SETSTATE ST_RECEIVE_READY
    ENDTEST
  ENDTEST
  TEST CamGen = ST_NOCHOOSETRACK
  OR CamGen = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_HOME
  TEST PalleStation = ST_HOME
  OR PalleStation = ST_WAIT_FOR_GATE
    SET goHome 0
  ENDTEST
  TEST parent = ST_IDLE
    SET goingHome 0
    SETSTATE ST_STARTUP
  ENDTEST
END

State=ST_RECEIVE_READY
  SET hold 0
  SET holdud 1
  TEST conveyorstation = ST_SEND
    SET conveyorenable 1
    SET conveyor receiveSpeed
    TIMEOUT enableConveyorTime ST_WAIT_ENABLE
    SETSTATE ST_WAIT_ENABLE
  ENDTEST
  TEST parent != ST_RUN
  AND testmode = 0
    SET waitSendAfterIdle 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_WAIT_ENABLE
  TEST enableConveyorTime = 1
    TIMEOUT maxReceiveTime ST_RECEIVE
    SETSTATE ST_RECEIVE
  ENDTEST
END

State=ST_RECEIVE
  TEST modtaget = ST_BLOCKED
    TIMEOUT waitStopConveyorTime ST_RECEIVE_STOP_CONVEYOR
    SETSTATE ST_RECEIVE_STOP_CONVEYOR
  ENDTEST
  TEST maxReceiveTime = 1
    SET errorCode 4111
    SET waitSendAfterIdle 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_RECEIVE_STOP_CONVEYOR
  TEST waitStopConveyorTime = 1
    SET conveyorenable 0
    SET conveyor 0.0
    TEST CamGen = ST_CHOOSETRACK
    AND conveyorstation != ST_SEND
    AND conveyorstation != ST_GO_BACK
      SETSTATE ST_WAIT_HOLD_POS
    ENDTEST
    TEST parent = ST_IDLE
      SETSTATE ST_IDLE
    ENDTEST
  ENDTEST
END

State=ST_WAIT_HOLD_POS
  TEST CamGen = ST_STOP
    TIMEOUT timeWaitHold ST_HOLD
    SETSTATE ST_TIMER
  ENDTEST
  TEST CamGen = ST_NOCHOOSETRACK
  OR CamGen = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_HOLD
  TEST goingHome = 1
    SET hold 0
    SET holdud 1
    TEST CamGen = ST_CHOOSETRACK
      SETSTATE ST_WAIT_RECEIVE_POS
    ENDTEST
    TEST parent = ST_IDLE
      SETSTATE ST_IDLE
    ENDTEST
  ELSE
    TEST tom = ST_BLOCKED
      SET holdud 0
      SET hold 1
      TEST casekind = casekind_Netto
        SET freeNetto 1
      ELSE
        SET freeOthers 1
      ENDTEST
    ELSE
      SET hold 0
      SET holdud 1
    ENDTEST
    TIMEOUT ventilTime ST_HOLD_VENTIL_WAIT_SEND
    SETSTATE ST_HOLD_VENTIL_WAIT_SEND
  ENDTEST
END

State=ST_HOLD_VENTIL_WAIT_SEND
  TEST ventilTime = 1
    TEST CamGen = ST_CHOOSETRACK
      SETSTATE ST_WAIT_SEND_POS
    ENDTEST
    TEST parent = ST_IDLE
      SETSTATE ST_IDLE
    ENDTEST
  ENDTEST
END

State=ST_WAIT_SEND_POS
  TEST stopFree = 1
    SET freeNetto 0
    SET freeOthers 0
  ENDTEST
  TEST CamGen = ST_STOP
    TEST testmode = 0
      SETSTATE ST_WAIT_SEND
    ELSE
      ;<testmode>
      TEST tom = ST_BLOCKED
        TEST modtaget = ST_BLOCKED
          SETSTATE ST_WAIT_HOLD_POS
        ELSE
          SETSTATE ST_WAIT_RELEASE_POS
        ENDTEST
      ELSE
        TEST modtaget = ST_BLOCKED
          SETSTATE ST_WAIT_HOLD_POS
        ELSE
          SETSTATE ST_WAIT_RECEIVE_POS
        ENDTEST
      ENDTEST
      ;</testmode>
    ENDTEST
    SET freeNetto 0
    SET freeOthers 0
  ENDTEST
  TEST CamGen = ST_NOCHOOSETRACK
  OR CamGen = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_WAIT_SEND
  TEST modtaget = ST_FREE
    ;Oops, no case. Might be stuck.
    SET errorCode 4115
    SET waitSendAfterIdle 0
    SETSTATE ST_IDLE
  ELSE
    TEST next = ST_EMPTY
      TIMEOUT maxSendTime ST_SEND
      SETSTATE ST_SEND
    ENDTEST
    TEST goHome = 1
    OR goingHome = 1
      ;go up and get cases and go home
      SET goingHome 1
      TEST CamGen = ST_CHOOSETRACK
        SETSTATE ST_WAIT_HOLD_POS
      ENDTEST
    ENDTEST
  ENDTEST
  TEST parent != ST_RUN
  AND testmode = 0
    SET waitSendAfterIdle 1
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_SEND
  TEST modtaget = ST_FREE
  AND afleveret = ST_FREE
    SET conveyorenable 0
    SET conveyor 0.0
    TEST CamGen = ST_CHOOSETRACK
      TEST tom = ST_BLOCKED
        SETSTATE ST_WAIT_RELEASE_POS
      ELSE
        SETSTATE ST_WAIT_RECEIVE_POS
      ENDTEST
    ENDTEST
  ELSE
    SET conveyorenable 1
    SET conveyor sendSpeed
  ENDTEST
  TEST maxSendTime = 1
    SET conveyor 0.0
    SET errorCode 4114
    SET waitSendAfterIdle 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_WAIT_RELEASE_POS
  TEST CamGen = ST_STOP
    SETSTATE ST_RELEASE
  ENDTEST
  TEST CamGen = ST_NOCHOOSETRACK
  OR CamGen = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_RELEASE
  SET hold 0
  SET holdud 1
  TIMEOUT ventilTime ST_HOLD_VENTIL_WAIT_HOLD
  SETSTATE ST_HOLD_VENTIL_WAIT_HOLD
END

State=ST_HOLD_VENTIL_WAIT_HOLD
  TEST ventilTime = 1
    TEST CamGen = ST_CHOOSETRACK
      SETSTATE ST_WAIT_HOLD_POS
    ENDTEST
    TEST parent = ST_IDLE
      SETSTATE ST_IDLE
    ENDTEST
  ENDTEST
END
