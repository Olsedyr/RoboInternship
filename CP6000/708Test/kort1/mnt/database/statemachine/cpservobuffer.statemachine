STATEMACHINEVERSION 3
Name=ServoBuffer
Link=Master BufferPlads
Link=parent Parent
Input=homesensor "BufferPlads HomeSensor"
Output=Mode "BufferPlads Mode"
Output=setpoint "BufferPlads MacSetpoint"
Output=vsoll "BufferPlads Maxvel"
Output=asoll "BufferPlads Maxacc"
Output=set_curpos "BufferPlads Curpos"
Output=kvout "BufferPlads Load Factor"
Output=set_status "BufferPlads Status"
Input=temperature "BufferPlads Temperature"
Input=Inpos "BufferPlads IN_POS"
Input=curpos "BufferPlads Curpos"
Input=curvel "BufferPlads Curvel"
Input=status "BufferPlads Status"
Value=errorCode 0
Value=curpm 0.0
Const=posBackOffset 3000
Const=posForwardOffset 97000
Const=loadfactor 2.5000
Value=homed 0
Const=homeSensorPol 0
Const=home_speed 400.0000
Const=acceleration 35000.0000
Const=cruise_speed 2700.0000
Const=testmode 0
Value=teststate 0
Value=distance 0.0
Value=startpos 0.0
Timeout=TimeTest 500000
Timeout=TimeCmdDelay 500000
Timeout=TimeStartMax 500000
Timeout=TimeHome 10000000

State=ST_TIMER
END

State=ST_HALT
END

State=ST_IDLE
	TEST parent = ST_RESET
    TEST status != 16
	  AND status != 32
	  AND status != 48
	  AND status != 64
	  AND status != 80
	  AND status != 0
      SET set_status 0
    ENDTEST
		SET errorCode 0
		SETSTATE ST_RESET
	ENDTEST
END

State=ST_RESET
	SET kvout loadfactor
	SET asoll acceleration
	TEST homed = 0
		SET vsoll home_speed
		SET Mode 1
		TIMEOUT TimeHome ST_HOME
		SETSTATE ST_HOME
	ELSE
		SET vsoll cruise_speed
		SET setpoint curpos
		TIMEOUT TimeCmdDelay ST_INIT
		SETSTATE ST_INIT
	ENDTEST
END

State=ST_HOME
	TEST homesensor = homeSensorPol
		SET Mode 0
		TIMEOUT TimeCmdDelay ST_HOME2
		SETSTATE ST_HOME2
	ELSETEST TimeHome = 1
		SET errorCode 1051
		SET Mode 0
		SETSTATE ST_IDLE
	ENDTEST
END

State=ST_HOME2
	TEST TimeCmdDelay = 1
		SET setpoint 0
		SET set_curpos 0
		SET homed 1
		SET Mode 2
		TIMEOUT TimeCmdDelay ST_HOME3
		SETSTATE ST_HOME3
	ENDTEST
END

State=ST_HOME3
	TEST TimeCmdDelay = 1
		SET vsoll cruise_speed
		SET setpoint posBackOffset
		SET startpos curpos
		SET distance posBackOffset
		TIMEOUT TimeStartMax ST_START
		SETSTATE ST_START
	ENDTEST
END

State=ST_INIT
	TEST TimeCmdDelay = 1
		SET Mode 2
		SETSTATE ST_CHOOSETRACK
	ENDTEST
END

State=ST_NOCHOOSETRACK
END

State=ST_CHOOSETRACK
	TEST Master = ST_IDLE
	AND testmode = 0
		SET Mode 0
		SETSTATE ST_IDLE
	ELSETEST Master = ST_BEGINCLOSE
		SET setpoint posBackOffset
		SET startpos curpos
		CALC distance = setpoint - curpos
		TIMEOUT TimeStartMax ST_START
		SETSTATE ST_START
	ELSETEST Master = ST_BEGINOPEN
		SET setpoint posForwardOffset
		SET startpos curpos
		CALC distance = setpoint - curpos
		TIMEOUT TimeStartMax ST_START
		SETSTATE ST_START
	ELSETEST testmode = 1
		TIMEOUT TimeTest ST_TEST
		SETSTATE ST_TEST
	ENDTEST
END

State=ST_TEST
	TEST TimeTest = 1
		TEST teststate = 0
			SET teststate 1
      ;forward
			SET setpoint posForwardOffset
			SET startpos curpos
			CALC distance = setpoint - curpos
			TIMEOUT TimeStartMax ST_START
			SETSTATE ST_START
		ELSETEST teststate = 1
			SET teststate 0
      ;back
			SET setpoint posBackOffset
			SET startpos curpos
			CALC distance = setpoint - curpos
			TIMEOUT TimeStartMax ST_START
			SETSTATE ST_START
    ENDTEST
	ENDTEST
END

State=ST_STOP
	SET curpm 1000.0
  SETSTATE ST_CHOOSETRACK
END

State=ST_START
	SET curpm 0.0
	TEST Inpos = 0
	OR TimeStartMax = 1
	  SETSTATE ST_RUN
  ENDTEST
END

State=ST_RUN
	CALC curpm = curpos - startpos
	CALC curpm = curpm / distance
	CALC curpm = curpm * 1000.0
	TEST Inpos = 1
    SETSTATE ST_STOP
  ENDTEST
  TEST status != 16
	AND status != 32
	AND status != 48
	AND status != 64
	AND status != 80
	AND status != 0
    SET Mode 0
    SET homed 0
    TIMEOUT TimeCmdDelay ST_IDLE
    SETSTATE ST_TIMER
  ENDTEST
END
