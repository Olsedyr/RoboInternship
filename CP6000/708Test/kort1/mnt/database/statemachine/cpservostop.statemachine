STATEMACHINEVERSION 3
Name=ServoStop
Link=Master KasseLift
Link=parent Parent
linkValue=firstRun WorkCell
Output=home_vel "KasseStop Homevel"
Output=home_tq "KasseStop Hometorque"
Output=home_mode "KasseStop Home Mode"
Input=InMode "KasseStop Mode"
Output=Mode "KasseStop Mode"
Output=setpoint "KasseStop MacSetpoint"
Output=vsoll "KasseStop Maxvel"
Output=asoll "KasseStop Maxacc"
Output=set_curpos "KasseStop Curpos"
Output=kvout "KasseStop Load Factor"
Output=set_status "KasseStop Status"
Input=Inpos "KasseStop IN_POS"
Input=curpos "KasseStop Curpos"
Input=status "KasseStop Status"
Value=errorCode 0
Value=gostoppos 0
Value=curpm 0.0
Const=loadfactor 1.6000
Value=homed 0
Value=temp 0
Const=home_speed 250.0000
Const=home_torque 25.0000
Const=acceleration 200000.0000
Const=cruise_speed 1800.0000
Const=testmode 0
Value=teststate 0
Value=distance 0.0
Value=startpos 0.0
Value=goIdle 0
; tickspermotorround*gearudveksling/(eff_rad*2*pi)
; eks: 4096*12/(50.93*2*pi)
Const=tickspermm 153.598738716922
; All posXX in mm.
Const=Offset 230.0
Const=posHome -10.0
Const=posDown -410.0000
Const=posTestBegin -390.0000
Const=posTestEnd -10.0000
linkValue=item Program
Timeout=TimeTest 200000
Timeout=TimeCmdDelay 500000
Timeout=TimeStartMax 1000000
Timeout=TimeHome 10000000

State=ST_TIMER
END

State=ST_HALT
END

State=ST_IDLE
	TEST parent = ST_RESET
    TEST status != 16
	  AND status != 32
	  AND status != 48
	  AND status != 64
	  AND status != 80
	  AND status != 0
      SET set_status 0
    ENDTEST
		SETSTATE ST_RESET
	ENDTEST
END

State=ST_RESET
	SET kvout loadfactor
	SET asoll acceleration
	TEST homed = 0
		SET home_vel home_speed
		SET home_tq home_torque
		SET temp 0
		TIMEOUT TimeCmdDelay ST_HOME
		SETSTATE ST_HOME
	ELSE
		SET vsoll cruise_speed
		SET setpoint curpos
		TIMEOUT TimeCmdDelay ST_INIT
		SETSTATE ST_INIT
	ENDTEST
END

State=ST_HOME
	TEST temp = 0
	AND TimeCmdDelay = 1
		SET Mode 12
		SET temp 1
		TIMEOUT TimeHome ST_HOME
		SETSTATE ST_HOME
	ELSETEST temp = 1
		TEST InMode = 12
			SET temp 2
		ENDTEST
	ELSETEST temp = 2
		TEST InMode = 0
			SET homed 1
			SET vsoll cruise_speed
			TIMEOUT TimeCmdDelay ST_HOME2
			SETSTATE ST_HOME2
		ELSETEST TimeHome = 1
			SET errorCode 1052
			SET Mode 0
			SETSTATE ST_IDLE
		ENDTEST
	ENDTEST
END

State=ST_HOME2
	TEST TimeCmdDelay = 1
		SET setpoint curpos
		TIMEOUT TimeCmdDelay ST_HOME3
		SETSTATE ST_HOME3
	ENDTEST
END

State=ST_HOME3
	TEST TimeCmdDelay = 1
		SET Mode 2
		TIMEOUT TimeCmdDelay ST_HOME4
		SETSTATE ST_HOME4
	ENDTEST
END

State=ST_HOME4
	TEST TimeCmdDelay = 1
		CALC setpoint = tickspermm * posHome
		SET startpos curpos
		CALC distance = setpoint - curpos
		TIMEOUT TimeStartMax ST_START
		SETSTATE ST_START
	ENDTEST
END

State=ST_INIT
	TEST TimeCmdDelay = 1
		SET Mode 2
		SETSTATE ST_CHOOSETRACK
	ENDTEST
END

State=ST_NOCHOOSETRACK
END

State=ST_CHOOSETRACK
	TEST testmode = 1
 		TIMEOUT TimeTest ST_TEST
		SETSTATE ST_TEST
	ELSETEST gostoppos = 0
		SET temp posDown
		CALC setpoint = tickspermm * temp
		SET startpos curpos
		CALC distance = setpoint - curpos
		TIMEOUT TimeStartMax ST_START
		SETSTATE ST_START
  ELSETEST gostoppos = 1
		CALC temp = Offset - item.gx
		CALC setpoint = tickspermm * temp
		SET startpos curpos
		CALC distance = setpoint - curpos
		TIMEOUT TimeStartMax ST_START
		SETSTATE ST_START
	ENDTEST
	TEST Master = ST_IDLE
  AND testmode = 0
    SET goIdle 1
		SET temp posDown
		CALC setpoint = tickspermm * temp
		SET startpos curpos
		CALC distance = setpoint - curpos
		TIMEOUT TimeStartMax ST_START
		SETSTATE ST_START
  ENDTEST
END

State=ST_TEST
	TEST TimeTest = 1
 		TEST teststate = 0
			SET teststate 1
			CALC setpoint = tickspermm * posTestBegin
			SET startpos curpos
			CALC distance = setpoint - curpos
			TIMEOUT TimeStartMax ST_START
			SETSTATE ST_START
		ELSETEST teststate = 1
			SET teststate 0
			CALC setpoint = tickspermm * posTestEnd
			SET startpos curpos
			CALC distance = setpoint - curpos
			TIMEOUT TimeStartMax ST_START
			SETSTATE ST_START
		ENDTEST
	ENDTEST
END

State=ST_STOP
	SET curpm 1000.0
	TEST Master = ST_IDLE
	AND goIdle = 1
    SET goIdle 0
		SET Mode 0
		SETSTATE ST_IDLE
  ELSE
		SETSTATE ST_CHOOSETRACK
	ENDTEST
END

State=ST_START
	SET curpm 0.0
	TEST Inpos = 0
	OR TimeStartMax = 1
	  SETSTATE ST_RUN
  ENDTEST
END

State=ST_RUN
	CALC curpm = curpos - startpos
	CALC curpm = curpm / distance
	CALC curpm = curpm * 1000.0
	TEST Inpos = 1
    SETSTATE ST_STOP
  ENDTEST
  TEST status != 16
	AND status != 32
	AND status != 48
	AND status != 64
	AND status != 80
	AND status != 0
    SET Mode 0
    SET homed 0
    TIMEOUT TimeCmdDelay ST_IDLE
    SETSTATE ST_TIMER
  ENDTEST
END
