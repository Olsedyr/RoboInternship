STATEMACHINEVERSION 3
Name=SyncConveyor1
;** Input **
;** Output **
Output=motorEnable "Sync Conveyor 1 Enable"
Output=motor "SyncConveyor1 Setpoint"
;** Link **
Link=foto SyncConveyor1Foto
Link=master CollectConveyor
;** linkValue **
linkValue=go goSync master
linkValue=prevETA ETA LongConveyor1
linkValue=fototest test foto
;** Const **
Const=motorSpeed 30.0000
Const=prevETAReceive 50000
Const=autoDetectItem 1
;** Value **
Value=errorCode 0
Value=myIdx 1
;** Timeout **
Timeout=maxReceiveTime 3000000
Timeout=sendTime 230000

State=ST_HALT
END

State=ST_TIMER
END

State=ST_IDLE
  SET motorEnable 0
  SET motor 0.0
  TEST master = ST_RESET
    SET errorCode 0
    SETSTATE ST_RESET
  ENDTEST
END

State=ST_RESET
  SET motor motorSpeed
  TEST fototest = 1
    SETSTATE ST_READY
  ELSE
    SETSTATE ST_EMPTY
  ENDTEST
END

State=ST_EMPTY
  SET motorEnable 1
  TEST fototest = 1
  AND autoDetectItem = 1
    SETSTATE ST_READY
  ELSE
    TEST prevETA < prevETAReceive
      TIMEOUT maxReceiveTime ST_RECEIVE
      SETSTATE ST_RECEIVE
;    ELSE
;      SET motorEnable 0
    ENDTEST
  ENDTEST
  TEST master = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_RECEIVE
  SET motorEnable 1
  TEST foto = ST_HIGH
    SETSTATE ST_READY
  ENDTEST
  TEST maxReceiveTime = 1
    SETSTATE ST_RESET
  ENDTEST
END

State=ST_READY
  TEST fototest = 0
  AND autoDetectItem = 1
    SETSTATE ST_EMPTY
  ELSE
    TEST go = myIdx
      TIMEOUT sendTime ST_SEND
      SETSTATE ST_SEND
    ELSE
      SET motorEnable 0
    ENDTEST
  ENDTEST
  TEST master = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_SEND
  SET motorEnable 1
  TEST foto = ST_LOW
  OR sendTime = 1
    SETSTATE ST_EMPTY
  ENDTEST
END
