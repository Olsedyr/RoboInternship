STATEMACHINEVERSION 3
Name=ServoLift
Link=Master Lift
Link=parent Parent
Link=Emergency Emergency
linkValue=firstRun WorkCell
linkValue=inShake Master

;read from MC224 by trio.c part off mpnSummary()
linkValue=moving mtype_f Robot
linkValue=curpos axis_f Robot
linkValue=followError fe_f Robot

;written to MC224 by trio.c
Value=enable 0
Const=speed 20.0000
Const=accel 21.0000
Const=decel 22.0000
Value=setpoint 0.0

Value=curpm 0.0
Value=temp 0
Const=testmode 0
Value=teststate 0
Value=distance 0.0
Value=startpos 0.0
Value=posf 0.0
Value=goIdle 0
Value=liftFree 1
;jogmode is controlled from formJogLiftUser.c
Value=jogmode 0
; All posXX in mm.
Const=posHome 10.0
;posShake > posFree
Const=posShake 70.0000
Const=posFree 60.0000
Const=posTop 378.0000
Const=posTestBegin 100.0000
Const=posTestEnd 157.0000
linkValue=item Program
Timeout=TimeTest 0

State=ST_TIMER
END

State=ST_HALT
END

State=ST_IDLE
	TEST parent = ST_RESET
		SETSTATE ST_RESET
	ENDTEST
END

State=ST_RESET
	CALC posf = posTop - item.dy
	CALC posf = posf - posFree
	SET setpoint curpos
  SETSTATE ST_INIT
  TEST Emergency != ST_POWERON
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_INIT
  SETSTATE ST_CHOOSETRACK
  TEST Emergency != ST_POWERON
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_NOCHOOSETRACK
END

State=ST_CHOOSETRACK
  TEST Master = ST_BEGINHOME
    SET setpoint posHome
		SET startpos curpos
		CALC distance = setpoint - curpos
		SETSTATE ST_START
  ELSE
    TEST Master = ST_BEGINHIGH
			CALC temp = posTop - item.dy
			TEST inShake = 1
				CALC temp = temp - posShake
			ENDTEST
			SET setpoint temp
			SET startpos curpos
			CALC distance = setpoint - curpos
			SETSTATE ST_START
    ELSE
			TEST testmode = 1
				TIMEOUT TimeTest ST_TEST
				SETSTATE ST_TEST
			ENDTEST
		ENDTEST
	ENDTEST
	TEST Master = ST_IDLE
  AND testmode = 0
  AND jogmode = 0
    SET goIdle 1
		SET setpoint posHome
		SET startpos curpos
		CALC distance = setpoint - curpos
		SETSTATE ST_START
  ENDTEST
	TEST curpos < posf
		SET liftFree 1
	ELSE
		SET liftFree 0
	ENDTEST
  TEST Emergency != ST_POWERON
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_TEST
	TEST TimeTest = 1
 		TEST teststate = 0
			SET teststate 1
			SET setpoint posTestBegin
			SET startpos curpos
			CALC distance = setpoint - curpos
			SETSTATE ST_START
		ELSE
			TEST teststate = 1
				SET teststate 0
				CALC temp = posTop - item.dy				
				SET setpoint temp
				SET startpos curpos
				CALC distance = setpoint - curpos
				SETSTATE ST_START
			ENDTEST
		ENDTEST
	ENDTEST
	TEST curpos < posf
		SET liftFree 1
	ELSE
		SET liftFree 0
	ENDTEST
  TEST Emergency != ST_POWERON
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_STOP
	SET curpm 1000.0
	TEST Master = ST_IDLE
	AND goIdle = 1
    SET goIdle 0
		SETSTATE ST_IDLE
  ELSE
		SETSTATE ST_CHOOSETRACK
	ENDTEST
  TEST Emergency != ST_POWERON
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_START
	SET curpm 0.0
	TEST moving != 0
	  SETSTATE ST_RUN
  ENDTEST
	TEST curpos < posf
		SET liftFree 1
	ELSE
		SET liftFree 0
	ENDTEST
  TEST Emergency != ST_POWERON
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_RUN
	CALC curpm = curpos - startpos
	CALC curpm = curpm / distance
	CALC curpm = curpm * 1000.0
	TEST moving = 0
    SETSTATE ST_STOP
  ENDTEST
	TEST curpos < posf
		SET liftFree 1
	ELSE
		SET liftFree 0
	ENDTEST  
  TEST Emergency != ST_POWERON
    SETSTATE ST_IDLE
  ENDTEST
END
