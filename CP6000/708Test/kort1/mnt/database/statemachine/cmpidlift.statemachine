STATEMACHINEVERSION 3
Name=PIDLift
;** Input **
Input=raw "KasseLift Encoder"
Input=powerok "KasseLift HZOK"
;** Output **
Output=motor "KasseLift Setpoint"
Output=encoderenable "KasseLift Input Validation"
Output=encoderreset "KasseLift Direct"
Output=motorenable "KasseLift Motor Enable"
Output=brakerelease "KasseLift Brake Release"
;** Link **
Link=CamGen CamGenLift
Link=parent Parent
;** linkValue **
linkValue=firstRun WorkCell
linkValue=setpoint CamGen
linkValue=setspeed CamGen
linkValue=camgenbeginning beginning CamGen
;** Table **
Table=pehistory 600
;** Item **
;** Const **
Const=maxhomeclicks 60
Const=minheight 0
Const=maxheight 33000
Const=home 2000
Const=Kp 6.0000
Const=Kd 15.0000
Const=Ki 0.3000
Const=Ks 0.0
Const=Kbias 0.0
Const=intdecay 0.9200
Const=minintegral -80000.0
Const=maxintegral 80000.0
Const=minoutval -60.0
Const=maxoutval 95.0
Const=factor 342.105
Const=dir 1.0
Const=motorstartval 0.0
Const=slowStopSpeed 0.72
Const=holdSpeed 0.75
Const=maxposerr 1500
Const=maxnegposerr -1500
Const=maxstaticposerr 200
Const=maxstaticnegposerr -200
;** Value **
Value=homeclicks 0
Value=oldsetpoint -10
Value=staticmode 0
Value=encoder 0
Value=outval 0.0
Value=poserr 0.0
Value=oldposerr 0.0
Value=integral 0.0
Value=diff 0.0
Value=Kpart 0.0
Value=Dpart 0.0
Value=Ipart 0.0
Value=Spart 0.0
Value=curpos 0.0
Value=homed 0
Value=errorCode 0
;** Timeout **
Timeout=TimeHomeGo 75000
Timeout=TimeHomeStop 400000
Timeout=TimeHomeEnd 500000
Timeout=TimeSlowStop 1000000
Timeout=TimeStatic 1000000
Timeout=TimeMotorFirstEnable 1000000
Timeout=TimeMotorEnable 250000
Timeout=TimeDisable 100000
Timeout=TimeBrakeRelease 20000

State=ST_TIMER
END

State=ST_HALT
END

State=ST_IDLE
  SET motor 0.0
  SET encoderreset 2
  SET motorenable 0
	SET brakerelease 0
  SET oldsetpoint -10
  SET staticmode 0
	CALC encoder = raw - home
  SET poserr 0.0
  SET integral 0.0
  ; don't SET encoderenable to 0
	TEST CamGen = ST_RESET
  OR CamGen = ST_NOCHOOSETRACK
		SETSTATE ST_RESET
	ENDTEST
  TEST parent = ST_RESET
    SET errorCode 0
  ENDTEST
END

State=ST_SLOWSTOP
	SET motor slowStopSpeed
	TEST TimeSlowStop = 1
		SETSTATE ST_IDLE
	ENDTEST
END

State=ST_RESET
  SET poserr 0.0
  SET integral 0.0
	SET oldsetpoint -10
	SET staticmode 0
  TEST homed = 1
		SET motorenable 1
		TIMEOUT TimeMotorFirstEnable ST_FIRST_ENABLE_WAIT
    SETSTATE ST_FIRST_ENABLE_WAIT
  ELSE
    SET encoderenable 1
    SETSTATE ST_STARTHOME
  ENDTEST
END

State=ST_FIRST_ENABLE_WAIT
	CALC encoder = raw - home
  TEST TimeMotorFirstEnable = 1
	  SET brakerelease 1
	  TIMEOUT TimeBrakeRelease ST_BRAKERELEASE_WAIT
    SETSTATE ST_BRAKERELEASE_WAIT
  ENDTEST
END

State=ST_RUN
	SET encoderreset 2
	CALC encoder = raw - home
  SET curpos encoder
  SET oldposerr poserr
  CALC poserr = setpoint - curpos
	SET pehistory poserr
  CALC diff = poserr - oldposerr
  CALC integral = poserr + integral
  CALC integral = integral * intdecay
  TEST integral < minintegral
    SET integral minintegral
  ELSE
    TEST integral > maxintegral
      SET integral maxintegral
    ENDTEST
  ENDTEST
  CALC Kpart = Kp * poserr
  CALC Dpart = Kd * diff
  CALC Ipart = Ki * integral
  CALC Spart = Ks * setspeed
  CALC outval = Dpart + Kpart
  CALC outval = Ipart + outval
  CALC outval = Spart + outval
	CALC outval = outval + Kbias
	CALC outval = outval / factor
  TEST outval < minoutval
    SET outval minoutval
  ELSE
    TEST outval > maxoutval
      SET outval maxoutval
    ENDTEST
  ENDTEST
  CALC outval = dir * outval
	TEST staticmode = 1
		TEST setpoint = oldsetpoint
    AND camgenbeginning = 0
			SET brakerelease 0
      TEST TimeDisable = 1
			  SET motor holdSpeed
        SET motorenable 0
			ELSE
				SET motor outval
      ENDTEST
		ELSE
      TEST camgenbeginning = 1
        SET staticmode 0
        TIMEOUT TimeMotorEnable ST_ENABLE_WAIT
        SETSTATE ST_ENABLE_WAIT
      ENDTEST
		ENDTEST
		SET oldsetpoint setpoint
	ELSE
		SET motor outval
		TEST TimeStatic = 1
			TEST setpoint = oldsetpoint
      AND CamGen != ST_RUN
        TEST poserr > maxstaticposerr
        OR poserr < maxstaticnegposerr
          ;04124 Fejl: Følgefejl på lift. Kasse/lift i klemme.
          SET errorCode 4124
          SET brakerelease 0
          SET motor 0.0
          SET motorenable 0
          SETSTATE ST_WAIT_CAMGEN_IDLE
        ELSE
				  SET staticmode 1
				  SET brakerelease 0
			    TIMEOUT TimeDisable ST_RUN
			    SETSTATE ST_RUN
        ENDTEST
			ELSE
				TEST powerok = 1
					SET brakerelease 1
          SET motorenable 1
				ENDTEST
        TIMEOUT TimeStatic ST_RUN
        SETSTATE ST_RUN
			ENDTEST
			SET oldsetpoint setpoint
		ENDTEST
	ENDTEST
  TEST CamGen = ST_IDLE
		SET brakerelease 0
		TIMEOUT TimeSlowStop ST_SLOWSTOP
		SETSTATE ST_SLOWSTOP
  ENDTEST
  TEST poserr > maxposerr
  OR poserr < maxnegposerr
    ;04124 Fejl: Følgefejl på lift. Kasse/lift i klemme.
    SET errorCode 4124
		SET brakerelease 0
    SET motor 0.0
    SET motorenable 0
    SETSTATE ST_WAIT_CAMGEN_IDLE
  ENDTEST
  TEST powerok = 0
		SET brakerelease 0
    SET motor 0.0
    SET motorenable 0
    SETSTATE ST_WAIT_CAMGEN_IDLE
	ENDTEST
END

State=ST_WAIT_CAMGEN_IDLE
  TEST CamGen = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_ENABLE_WAIT
	CALC encoder = raw - home
  SET motor holdSpeed
  SET motorenable 1
  TEST TimeMotorEnable = 1
	  TIMEOUT TimeBrakeRelease ST_BRAKERELEASE_WAIT
    SETSTATE ST_BRAKERELEASE_WAIT
  ENDTEST
END

State=ST_BRAKERELEASE_WAIT
	CALC encoder = raw - home
	SET brakerelease 1
  TEST TimeBrakeRelease = 1
    ;reset
    SET poserr 0.0
    SET integral 0.0
    TIMEOUT TimeStatic ST_RUN
    SETSTATE ST_RUN    
  ENDTEST
	TEST powerok = 0
		SET brakerelease 0
    SET motorenable 0
	ENDTEST
END

State=ST_STARTHOME
	SET homed 0
	SET homeclicks 0
  SET motorenable 0
	SET motor 0.0
	SET brakerelease 0
	SET encoderreset 2
	TIMEOUT TimeHomeStop ST_HOME
	SETSTATE ST_HOME
END

State=ST_HOME 
	TEST homeclicks >= maxhomeclicks
		SETSTATE ST_ENDHOME
	ENDTEST
  TEST TimeHomeStop = 1
		SET brakerelease 1
		TIMEOUT TimeHomeGo ST_HOME2
		SETSTATE ST_HOME2
  ENDTEST
END

State=ST_HOME2
  TEST TimeHomeGo = 1
		SET brakerelease 0
		CALC homeclicks = homeclicks + 1
		TIMEOUT TimeHomeStop ST_HOME
		SETSTATE ST_HOME
  ENDTEST
END

State=ST_ENDHOME
	SET brakerelease 1
	TIMEOUT TimeHomeEnd ST_ENDHOME2
  SETSTATE ST_ENDHOME2
END

State=ST_ENDHOME2
	SET encoderreset 6
  SET motorenable 1
	SET motor 0.0
  SET brakerelease 1
	TEST TimeHomeEnd = 1
		SET homed 1
	  SET encoderreset 2
	  CALC encoder = raw - home
		TIMEOUT TimeStatic ST_RUN
		SETSTATE ST_RUN
	ENDTEST
END
