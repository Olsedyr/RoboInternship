STATEMACHINEVERSION 3
Name=Pallebane7
Input=palletInPlace "Pallebane7 Foto"
Output=Motor "Pallebane7 Motor"
Link=next Strapper
Link=prev Pallebane6
Link=parent Parent 
Timeout=maxDelayStop 10000000
Timeout=StartupTimeout 5000000
Timeout=StopDelay 0
Timeout=PickupDelay 0
Timeout=ReadyDelay 0
Const=autoDetectPallet 0
Const=ON 1
linkValue=strapEnable6 ProgramB1
linkValue=palletStackable6 ProgramB1
Value=strapEnable7 0
Value=palletStackable7 0

;en pallebane går automatisk fra ready til empty hvis fotocelle slukker
;en pallebane går automatisk fra empty til ready hvis fotocelle tænder


State=ST_HALT
END

State=ST_TIMER
END

State=ST_IDLE
  SET Motor 0
  TEST parent = ST_RESET
  OR parent = ST_RUN
    SETSTATE ST_RESET
  ENDTEST
END

State=ST_READY
  TEST palletInPlace != ON 
  AND autoDetectPallet = 1
    TIMEOUT PickupDelay ST_EMPTY
    SETSTATE ST_TIMER
  ELSE
    TEST next = ST_EMPTY
      TIMEOUT maxDelayStop ST_SEND
      SETSTATE ST_SEND
    ELSE 
      SET Motor 0
    ENDTEST
  ENDTEST
  TEST parent = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_SEND
  TEST next = ST_READY
    SETSTATE ST_WAITSTRAP
  ELSE
    TEST maxDelayStop = 1
      SETSTATE ST_RESET
    ELSE
      TEST next = ST_RECEIVE
        SET Motor 1
      ENDTEST
    ENDTEST
  ENDTEST
END

State=ST_WAITSTRAP
	TEST next = ST_EMPTY
	OR next = ST_NOTREADY
		SETSTATE ST_EMPTY
	ELSE
		TEST next = ST_SEND
			SET Motor 1
		ELSE
			SET Motor 0
		ENDTEST
	ENDTEST
END

State=ST_EMPTY
  TEST palletInPlace = ON 
  AND autoDetectPallet = 1
    TIMEOUT ReadyDelay ST_READY
    SETSTATE ST_TIMER
  ELSE
    TEST prev = ST_SEND
      TIMEOUT maxDelayStop ST_RECEIVE
      SETSTATE ST_RECEIVE
    ELSE 
      SET Motor 0
    ENDTEST
  ENDTEST
  TEST parent = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_RECEIVE
  TEST palletInPlace = ON
		SET strapEnable7 strapEnable6
		SET strapEnable6 0
		SET palletStackable7 palletStackable6
		SET palletStackable6 0
    TIMEOUT StopDelay ST_READY
    SETSTATE ST_TIMER
  ELSE
    TEST maxDelayStop = 1
      SETSTATE ST_RESET
    ELSE
      TEST prev = ST_SEND
        SET Motor 1
      ENDTEST
    ENDTEST
  ENDTEST
END

State=ST_RESET
  TEST palletInPlace = ON
    SETSTATE ST_READY
  ELSE
    TIMEOUT StartupTimeout ST_STOP
    SETSTATE ST_STOP
  ENDTEST
END

State=ST_STOP
  TEST palletInPlace = ON
    SETSTATE ST_READY
  ELSE
    TEST StartupTimeout = 1
      SETSTATE ST_EMPTY
    ELSE
			TEST StartupTimeout < 2000000
			AND next = ST_EMPTY
				SET Motor 1
			ENDTEST
    ENDTEST
  ENDTEST
END

