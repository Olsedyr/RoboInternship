STATEMACHINEVERSION 3
Name=PallebanePS
;** Input **
Input=palletInPlace "Pallebane6 FotoRev"
Input=fotoAlign "Pallebane6 FotoAlign"
Input=emptyPallet "Pallebane6 FotoTest"
;** Output **
Output=MotorFwd "Pallebane6 MotorFwd"
Output=MotorRev "Pallebane6 MotorRev"
Output=MotorSoft "dummyOut"
Output=ChainFwd "Pallebane6 Chain"
Output=ChainRev "Pallebane6 Chain"
Output=Cyl "Pallebane6 Cyl"
;** Link **
Link=next Pallebane7
Link=prev Pallemagasin 
Link=parent Parent
Link=PS PS
;** linkValue **
linkValue=firstRun WorkCell
linkValue=nextpalb Frames
linkValue=moveBottom PS
linkValue=moveTop PS
;** Const **
Const=usePrev 1
Const=usePrev2 0
Const=ON 1
Const=useEmptyDelay 1
;** Value **
Value=simulate 0
Value=errorCode 0
Value=cPallet 0
;** Timeout **
Timeout=TimeoutDelay 5000000
Timeout=TimeoutForPalletRev 2000000
Timeout=TimeoutForPalletSideAlign 4000000
Timeout=TimeoutForPalletLift 2000000
Timeout=maxDelayStop 10000000
Timeout=TimeoutRev 150000
Timeout=EmptyDelay 10000000

State=ST_HALT
END

State=ST_TIMER
 END

State=ST_IDLE
  SET MotorFwd 0
  SET MotorRev 0
  SET ChainFwd 0
  SET ChainRev 0
  SET Cyl 0
  TEST parent = ST_RESET
  OR simulate = 1
    SET errorCode 0
    SETSTATE ST_RESET
  ENDTEST
END

;*******************************************
;* PALLET IS NOT EMPTY, SHOULD IT BE SEND
;* OUT OF WORKCELL AND MAKE ROOM FOR A NEW 
;* ask boxcounter.statemachine how many boxes 
;* are on pallet and set cBItems
;*******************************************
State=ST_PALLET_NOT_EMPTY
  TEST nextpalb = 1
    SETSTATE ST_GET_READY1
  ELSE
    SETSTATE ST_READY_ROBOT
  ENDTEST
END
;*******************************************
;* PHOTOCELL HAS TOLD US THAT PALLET IS EMPTY
;* SHOULD WE DO MORE TESTING TO BE SURE ?
;* THIS STATE SHOULD BE RESPONSIBLE FOR 
;* THE CORRECT VALUE of cBItems
;*******************************************
State=ST_PALLET_EMPTY
  TEST PS = ST_TOP
    SET moveBottom 1
    SETSTATE ST_PALLET_EMPTY2
  ENDTEST
END

State=ST_PALLET_EMPTY2
  TEST PS = ST_FOTO_FRI
  OR PS = ST_HP_READY
  	SET nextpalb 0
    SETSTATE ST_READY_ROBOT
  ENDTEST
;TODO timeout timeToFotoFri
END

State=ST_READY_ROBOT
	TEST nextpalb = 1
		SETSTATE ST_GET_READY1
	ENDTEST
END

State=ST_GET_READY1
  SET moveTop 1
  SETSTATE ST_GET_READY2
END

State=ST_GET_READY2
  TEST PS = ST_TOP
		TEST palletInPlace = ON
		AND emptyPallet = 1
      SETSTATE ST_PALLET_EMPTY
    ELSE
      SETSTATE ST_READY
    ENDTEST
  ENDTEST
;TODO timeout timeToTop
END

State=ST_READY
  TEST next = ST_EMPTY
    TIMEOUT maxDelayStop ST_SEND
    SETSTATE ST_SEND
  ELSE
    SET MotorFwd 0
    SET MotorSoft 0
    SET MotorRev 0
    SET ChainFwd 0
    SET ChainRev 0
    SET Cyl 0
    SET MotorRev 0
  ENDTEST
  TEST parent = ST_IDLE
  AND simulate = 0
    SETSTATE ST_IDLE
  ENDTEST  
END

State=ST_SEND
  TEST next = ST_READY
  OR simulate = 1
    CALC cPallet = cPallet + 1
    SETSTATE ST_EMPTY
  ELSE
    SET MotorFwd 1
    SET MotorSoft 1
  ENDTEST
  TEST parent = ST_IDLE
  AND maxDelayStop = 1
  AND simulate = 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_EMPTY
  SET MotorFwd 0
  SET MotorSoft 0
  SET MotorRev 0
  SET ChainFwd 0
  SET ChainRev 0
  SET Cyl 0
  TEST prev = ST_READY
    SET Cyl 1
    TIMEOUT TimeoutForPalletLift ST_EMPTY_SHIFT2
    SETSTATE ST_TIMER
  ENDTEST
  TEST prev = ST_EMPTY
  AND useEmptyDelay = 1
    TIMEOUT EmptyDelay ST_TESTEMPTY
    SETSTATE ST_TESTEMPTY 
  ENDTEST
  TEST parent = ST_IDLE
  AND simulate = 0
    SETSTATE ST_IDLE
  ENDTEST  
  TEST palletInPlace = ON 
    SET errorCode 3013
    SETSTATE ST_IDLE
;     palleplads ikke tom
  ENDTEST  
END

State=ST_TESTEMPTY
  TEST prev != ST_EMPTY
    SETSTATE ST_EMPTY
  ELSE
    TEST EmptyDelay = 1
      SET errorCode 1001
;pallet missing
      SETSTATE ST_IDLE
    ENDTEST
  ENDTEST
END

State=ST_EMPTY_SHIFT2
  TIMEOUT maxDelayStop ST_RECEIVE2
  SETSTATE ST_RECEIVE2
END

State=ST_RECEIVE2
  TEST prev = ST_SEND
    SET ChainRev 1
  ENDTEST
  TEST fotoAlign = 1
  OR simulate = 1
    TIMEOUT TimeoutForPalletSideAlign ST_PALLET_SIDE_ALIGN_TIMEOUT2
    SETSTATE ST_PALLET_SIDE_ALIGN_TIMEOUT2
  ELSE
    TEST maxDelayStop = 1
;     pallet stuck
      TEST parent = ST_IDLE
      AND simulate = 0
        SET errorCode 3014
        SETSTATE ST_IDLE
      ENDTEST  
    ENDTEST
  ENDTEST
END

State=ST_PALLET_SIDE_ALIGN_TIMEOUT2
  TEST TimeoutForPalletSideAlign = 1
    SET ChainRev 0
    SET Cyl 0
    SETSTATE ST_PALLET_REV
  ENDTEST
END

State=ST_PALLET_REV
  SET MotorRev 1
  SET MotorSoft 1
  TEST palletInPlace = ON
  OR simulate = 1
    TIMEOUT TimeoutForPalletRev ST_PALLET_REV_TIMEOUT
    SETSTATE ST_TIMER
  ENDTEST
  TEST parent = ST_IDLE
  AND simulate = 0
    SETSTATE ST_IDLE
  ENDTEST  
END

State=ST_PALLET_REV_TIMEOUT
  SET MotorRev 0
  SET MotorSoft 0
  SETSTATE ST_PALLET_EMPTY
END

State=ST_RESET
	TEST PS = ST_TOP
		TEST palletInPlace = ON
		AND emptyPallet = 0
			SETSTATE ST_PALLET_NOT_EMPTY
		ELSE 
			SET MotorRev 1
			SET MotorSoft 1
			TIMEOUT TimeoutDelay ST_STOP
			SETSTATE ST_STOP
		ENDTEST
		TEST simulate = 1 
			SETSTATE ST_PALLET_EMPTY
		ENDTEST
	ENDTEST
  TEST parent = ST_IDLE
  AND simulate = 0
    SETSTATE ST_IDLE
  ENDTEST  
END

State=ST_STOP
  TEST palletInPlace = ON
    TIMEOUT TimeoutForPalletRev ST_PALLET_REV_TIMEOUT_STOP
    SETSTATE ST_TIMER
  ELSE 
    TEST TimeoutDelay = 1
      SETSTATE ST_EMPTY
    ENDTEST	
  ENDTEST
END

State=ST_PALLET_REV_TIMEOUT_STOP
  SET MotorRev 0
  SET MotorSoft 0
  TEST emptyPallet = 1
    SETSTATE ST_PALLET_EMPTY
  ELSE
    SETSTATE ST_PALLET_NOT_EMPTY
  ENDTEST
END

