STATEMACHINEVERSION 3
Name=ConveyorStation
;** Input **
;** Output **
Output=motor "ConveyorStation Motor Enable"
Output=motorSetpoint "ConveyorStation Setpoint"
Output=ventil "ConveyorStation Centrer Ventil"
;following two only for testing purpose (simulate = 1):
Output=liftconveyor "KasseStativConveyor Setpoint"
Output=liftconveyorenable "KasseLiftConveyor Motor Enable"
;** Link **
Link=stabel1 ConveyorStationFotoStabel1
Link=stabel2 ConveyorStationFotoStabel2
Link=fremme ConveyorStationFotoFremme
Link=pallestation PalleStation
Link=kassestativ KasseStativ
Link=parent Parent
;** linkValue **
linkValue=pallestationsending sending PalleStation
linkValue=motorSpeed receiveSpeed KasseStativ
;** Table **
;** Item **
;** Const **
;** Value **
Value=errorCode 0
Value=sending 0
Value=simulate 0
Value=temp 0.0
Value=goBack 0
;** Timeout **
Timeout=sendTime 1800000
Timeout=maxReceiveTime 20000000
Timeout=maxGoFremTime 10000000
Timeout=centrerTime 2000000
Timeout=motorStopTime 1000000
Timeout=fremmeExtraTime 600000
Timeout=backExtraTime 500000
Timeout=motorFirstEnableTime 1000000

State=ST_TIMER
END

State=ST_HALT
END

State=ST_IDLE
  SET motor 0
  SET ventil 0
  TEST parent = ST_RESET
    SET errorCode 0
    SET motor 1
    SET motorSetpoint 0.0
    TIMEOUT motorFirstEnableTime ST_RESET
    SETSTATE ST_TIMER
  ENDTEST
END

State=ST_RESET
  SET motor 0
  SET motorSetpoint motorSpeed
  TEST simulate = 1
    SET liftconveyorenable 1
    SET liftconveyor motorSpeed
  ENDTEST
  SETSTATE ST_RUNNING
END

State=ST_RUNNING
  TEST stabel1 = ST_FREE
  AND stabel2 = ST_FREE
    SETSTATE ST_RECEIVE_READY
  ENDTEST
  TEST fremme = ST_BLOCKED
    TEST kassestativ = ST_RECEIVE_READY
    OR simulate = 1
      SETSTATE ST_SEND
    ENDTEST
  ELSE
    TEST fremme = ST_FREE
      TEST stabel1 = ST_BLOCKED
      OR stabel2 = ST_BLOCKED
        TIMEOUT maxGoFremTime ST_GO_FREM
        SETSTATE ST_GO_FREM
      ENDTEST
    ENDTEST
  ENDTEST
  TEST parent != ST_RUN
  AND simulate = 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_RECEIVE_READY
  TEST kassestativ = ST_RECEIVE_READY
  OR simulate = 1
    TEST fremme = ST_BLOCKED
      SETSTATE ST_SEND
    ENDTEST
  ENDTEST
  TEST pallestationsending = 1
  OR simulate = 1
    TIMEOUT maxReceiveTime ST_RECEIVE
    SETSTATE ST_RECEIVE
  ENDTEST
  TEST stabel1 = ST_BLOCKED
  OR stabel2 = ST_BLOCKED
    SET errorCode 4104
    SETSTATE ST_IDLE
  ENDTEST
  TEST parent != ST_RUN
  AND simulate = 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_RECEIVE
  SET motor 0
  TEST stabel1 = ST_BLOCKED
  OR stabel2 = ST_BLOCKED
    TEST pallestation = ST_POS3
    OR pallestation = ST_POS2
    OR pallestationsending = 0
      SETSTATE ST_RUNNING
    ENDTEST
  ENDTEST
  TEST maxReceiveTime = 1
    SET errorCode 4103
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_SEND
  TEST sending = 0
    TEST kassestativ = ST_RECEIVE
    OR simulate = 1
      SET motor 1
      SET sending 1
      TIMEOUT sendTime ST_SEND
      SETSTATE ST_SEND
    ENDTEST
  ELSE
    TEST sendTime = 1
      SET sending 0
      SET motor 0
      TEST fremme = ST_BLOCKED
      OR goBack = 1
        TIMEOUT motorStopTime ST_GO_BACK
        SETSTATE ST_TIMER
        ;TIMEOUT motorStopTime ST_STOP_MOTOR_CENTRER
        ;SETSTATE ST_STOP_MOTOR_CENTRER
      ELSE
        TIMEOUT motorStopTime ST_RUNNING
        SETSTATE ST_TIMER
      ENDTEST
    ENDTEST
  ENDTEST
  TEST fremme = ST_FREE
    TEST stabel1 = ST_BLOCKED
    OR stabel2 = ST_BLOCKED
      SET goBack 1
;			SET sending 0
;			SETSTATE ST_GO_FREM
		ENDTEST
  ENDTEST
END

State=ST_GO_BACK
  SET goBack 0
  CALC temp = -1 * motorSpeed
  SET motorSetpoint temp
  SET motor 1
  TEST fremme = ST_FREE
    TIMEOUT backExtraTime ST_GO_FURTHER_BACK
    SETSTATE ST_GO_FURTHER_BACK
  ENDTEST
END

State=ST_GO_FURTHER_BACK
  TEST backExtraTime = 1
    SET motor 0
    SET motorSetpoint motorSpeed
    TIMEOUT maxGoFremTime ST_GO_FREM
    SETSTATE ST_GO_FREM
  ENDTEST
END

State=ST_GO_FREM
  SET motor 1
  TEST fremme = ST_BLOCKED
    TIMEOUT fremmeExtraTime ST_WAIT_MOTOR_STOP
    SETSTATE ST_WAIT_MOTOR_STOP
  ENDTEST
  TEST maxGoFremTime = 1
    SET errorCode 4105
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_WAIT_MOTOR_STOP
  TEST fremmeExtraTime = 1
    SET motor 0
    TIMEOUT motorStopTime ST_STOP_MOTOR_CENTRER
    SETSTATE ST_STOP_MOTOR_CENTRER
  ENDTEST
END

State=ST_STOP_MOTOR_CENTRER
  TEST motorStopTime = 1
    SET ventil 1
    TIMEOUT centrerTime ST_CENTRER
    SETSTATE ST_CENTRER
  ENDTEST
END

State=ST_CENTRER
  TEST centrerTime = 1
    SET ventil 0
    TIMEOUT centrerTime ST_UCENTRER
    SETSTATE ST_UCENTRER
  ENDTEST
END

State=ST_UCENTRER
  TEST centrerTime = 1
    SETSTATE ST_RUNNING
  ENDTEST
END
