STATEMACHINEVERSION 3
Name=PIDSkub
Delaystop=10000000
Input=raw "LangBufferSkub Encoder"
Input=homesensor "LangBufferSkub HomeSensor"
Output=Motor "LangBufferSkub Setpoint"
Output=encoderenable "LangBufferSkub Input Validation"
Output=encoderreset "LangBufferSkub Direct"
Output=motorenable "LangBufferSkub Motor Enable"
Output=analogMotor "LangBufferSkub DAC"
Link=CamGen CamGenSkub
Link=parent Parent
Value=errorCode 0
Table=pehistory 600
linkValue=firstRun WorkCell
linkValue=setpoint CamGen
linkValue=setspeed CamGen
Value=ticksround 20000
Value=ticksroundh 8000
Const=home 10000
Const=speedinhoming 4.0000
Value=homevalone 0
Value=homevaltwo 0
Value=homevalthree 0
Const=Kp 5.0000
Const=Kd 18.0000
Const=Ki 0.2200
Const=Ks 0.4000
Const=intdecay 0.93
Const=minintegral -100000.0000
Const=maxintegral 100000.0000
Const=minoutval -110.0
Const=maxoutval 110.0
Const=factor 295.45
Const=dir 1
Value=encoder 0
Value=outval 0.0
Value=poserr 0
Value=oldposerr 0
Value=integral 0.0
Value=diff 0
Value=temp 0
Value=Kpart 0.0
Value=Dpart 0.0
Value=Ipart 0.0
Value=Spart 0.0
Value=curpos 0.0
Value=homed 0
Timeout=TimeHome 20000000
Timeout=TimeStartup 800000

; Frekvensomformer setup
; Param 01: 0.0
; Param 02: 200 (Max set speed [Hz])
; Param 03,04: 0.0 (acceleration/decceleration limits)
; Param 05: AV.Pr (Voltage Input and 3 preset speeds)
; Param 06: 1.4 (rated current [A])
; Param 07: 1400 (Motor rated speed [RPM])
; Param 08: 230 (Rated Voltage [Vrms])
; Param 09: 0.73 (Motor Power factor aka cos phi)
; Param 10: (Level 2, access to params 01-60)
; Param 38: 1 (non-rotating static auto-tune) - nÃ¸dstop->reset->motor enable
; Param 39: 50 (Motor rated frequency [Hz]) 
; Param 40: 4P (4 pole motor)

State=ST_TIMER
END

State=ST_HALT
END

State=ST_IDLE
  SET Motor 0.0
  SET encoderreset 0
  SET motorenable 0
  ; don't SET encoderenable to 0
	TEST parent = ST_RESET
		SETSTATE ST_RESET
	ENDTEST
END

State=ST_RESET
	TEST firstRun = 1
		SET homed 0
	ENDTEST
  SET motorenable 1
  TEST homed = 1
		TIMEOUT TimeStartup ST_STARTUP
		SETSTATE ST_STARTUP
  ELSE
    SET encoderenable 3
    SETSTATE ST_STARTHOME
  ENDTEST
END

State=ST_STARTUP
	SET motorenable 1
  SET Motor 0.0
	TEST TimeStartup = 1
		SETSTATE ST_RUN
	ENDTEST
END

State=ST_RUN
	; should we reset counter?
  TEST homesensor = 0
		SET encoderreset 4
	ELSE
		SET encoderreset 0
	ENDTEST
	; make absolute encoder
	TEST raw < home
    CALC encoder = ticksround - home 
    CALC encoder = encoder + raw
  ELSE
    CALC encoder = raw - home
    CALC encoder = encoder % ticksround
  ENDTEST
  SET curpos encoder
  SET oldposerr poserr
  CALC poserr = setpoint - curpos
  TEST poserr > ticksroundh
    CALC poserr = poserr - ticksround
  ELSE
    CALC temp = poserr * -1
    TEST temp > ticksroundh
      CALC poserr = poserr + ticksround
    ENDTEST
  ENDTEST
	SET pehistory poserr
  CALC diff = poserr - oldposerr
  CALC integral = poserr + integral
  CALC integral = integral * intdecay
  TEST integral < minintegral
    SET integral minintegral
  ELSE
    TEST integral > maxintegral
      SET integral maxintegral
    ENDTEST
  ENDTEST
  CALC Kpart = Kp * poserr
  CALC Dpart = Kd * diff
  CALC Ipart = Ki * integral
  CALC Spart = Ks * setspeed
  CALC outval = Dpart + Kpart
  CALC outval = Ipart + outval
  CALC outval = Spart + outval
  CALC outval = dir * outval
	CALC outval = outval / factor
  TEST outval < minoutval
    SET outval minoutval
  ELSE
    TEST outval > maxoutval
      SET outval maxoutval
    ENDTEST
  ENDTEST
  SET Motor outval
  TEST CamGen = ST_IDLE
		SETSTATE ST_IDLE
  ENDTEST
END

State=ST_STARTHOME
  SET encoderreset 0
  TIMEOUT TimeHome ST_HOME
  SETSTATE ST_HOME
END

State=ST_HOME
  TEST homesensor = 0
    SET encoderreset 4
    SET homevalone home
    SET Motor speedinhoming
    SETSTATE ST_HOME2
  ELSE
    SET Motor speedinhoming
  ENDTEST
  TEST TimeHome = 1
    SET errorCode 1050
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_HOME2
  TEST homesensor = 1
    SET encoderreset 0
    SET homevaltwo raw
    SET Motor speedinhoming
    SETSTATE ST_HOME3
  ELSE
    SET Motor speedinhoming
  ENDTEST
END

State=ST_HOME3
  TEST homesensor = 0
    SET encoderreset 4
    SET homevalthree raw
    SET homed 1
    SET Motor 0.0
    CALC ticksround = homevalthree - homevalone
    CALC ticksroundh = ticksround / 3
    SETSTATE ST_RUN
  ELSE
    SET Motor speedinhoming
  ENDTEST
END
