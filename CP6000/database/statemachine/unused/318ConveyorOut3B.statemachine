STATEMACHINEVERSION 3
Name=ConveyorOut3B
linkValue=photo test ConveyorOut3BPhoto
Output=motor "Conveyor POS.1.10 Motor"
Link=parent Parent
Link=next ConveyorOut2B
Value=simulate 0
Value=sending 0
Timeout=startupTime 2000000

State=ST_TIMER
END

State=ST_HALT
END

State=ST_IDLE
  SET motor 0
  TEST parent = ST_RESET
  OR simulate = 1
    SETSTATE ST_RESET
  ENDTEST
END

State=ST_RESET
  TIMEOUT startupTime ST_STARTUP
  SETSTATE ST_STARTUP
END

State=ST_STARTUP
  TEST photo = 1
    SETSTATE ST_READY
  ELSE
    TEST startupTime = 1
      SETSTATE ST_GET
    ELSE
      SET motor 1
    ENDTEST
  ENDTEST
END

State=ST_GET
  TEST photo = 1
    SETSTATE ST_READY
  ELSE
    SET motor 1
  ENDTEST
  TEST parent = ST_IDLE
  AND simulate = 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_READY
  TEST photo = 0
    SETSTATE ST_GET
  ELSETEST next = ST_EMPTY
    SETSTATE ST_SEND
  ELSE
    SET motor 0
  ENDTEST
  TEST parent = ST_IDLE
  AND simulate = 0
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_SEND
  TEST sending = 0
    TEST next = ST_RECEIVE
      SET sending 1
      SET motor 1
    ELSETEST next = ST_IDLE
      SET sending 0
      SETSTATE ST_IDLE
    ENDTEST
  ELSE
    TEST next != ST_RECEIVE
    AND next != ST_EMPTY
      SET sending 0
      SETSTATE ST_GET
    ENDTEST
  ENDTEST
END
