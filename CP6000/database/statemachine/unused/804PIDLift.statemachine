STATEMACHINEVERSION 3
Name=PIDLift
Delaystop=10000000
Input=raw "KasseLift Encoder"
Input=powerok "KasseLift HZOK"
Output=Motor "KasseLift Setpoint"
Output=encoderenable "KasseLift Input Validation"
Output=encoderreset "KasseLift Direct"
Output=motorenable "KasseLift Motor Enable"
Output=brakerelease "KasseLift Brake Release"
Link=CamGen CamGenLift
Link=parent Parent
linkValue=firstRun WorkCell
linkValue=setpoint CamGen
linkValue=setspeed CamGen
Table=pehistory 600
Const=maxhomeclicks 60
Value=homeclicks 0
Const=minheight 0
Const=maxheight 33000
Const=home 2000
Const=Kp 5.0000
Const=Kd 15.0000
Const=Ki 0.3000
Const=Ks 0.0
Const=Kbias 0.0
Const=intdecay 0.9200
Const=minintegral -80000.0
Const=maxintegral 80000.0
Const=minoutval -40.0000
Const=maxoutval 40.0000
Const=factor 342.105
Const=dir 1.0000
Const=motorstartval -0.8
Value=oldsetpoint -10
Value=staticmode 0
Value=encoder 0
Value=outval 0.0
Value=poserr 0.0
Value=oldposerr 0.0
Value=integral 0.0
Value=diff 0.0
Value=Kpart 0.0
Value=Dpart 0.0
Value=Ipart 0.0
Value=Spart 0.0
Value=curpos 0.0
Value=homed 0
Timeout=TimeHomeGo 75000
Timeout=TimeHomeStop 400000
Timeout=TimeHomeEnd 500000
Timeout=TimeSlowStop 1000000
Timeout=TimeStatic 1000000
Timeout=TimeDisable 0
Const=SlowStopSpeed 0.263

State=ST_TIMER
END

State=ST_HALT
END

State=ST_IDLE
  SET Motor 0.0
  SET encoderreset 2
  SET motorenable 0
	SET brakerelease 0
  ; don't SET encoderenable to 0
	TEST parent = ST_RESET
		SETSTATE ST_RESET
	ENDTEST
END

State=ST_SLOWSTOP
	SET Motor SlowStopSpeed
	TEST TimeSlowStop = 1
		SETSTATE ST_IDLE
	ENDTEST
END

State=ST_RESET
	SET oldsetpoint -10
	SET staticmode 0
	TEST firstRun = 1
		SET homed 0
	ENDTEST
  TEST homed = 1
		SET motorenable 1
		SET brakerelease 1
		TIMEOUT TimeStatic ST_RUN
		SETSTATE ST_RUN
  ELSE
    SET encoderenable 1
    SETSTATE ST_STARTHOME
  ENDTEST
END

State=ST_RUN
	SET encoderreset 2
	CALC encoder = raw - home
  SET curpos encoder
  SET oldposerr poserr
  CALC poserr = setpoint - curpos
	SET pehistory poserr
  CALC diff = poserr - oldposerr
  CALC integral = poserr + integral
  CALC integral = integral * intdecay
  TEST integral < minintegral
    SET integral minintegral
  ELSE
    TEST integral > maxintegral
      SET integral maxintegral
    ENDTEST
  ENDTEST
  CALC Kpart = Kp * poserr
  CALC Dpart = Kd * diff
  CALC Ipart = Ki * integral
  CALC Spart = Ks * setspeed
  CALC outval = Dpart + Kpart
  CALC outval = Ipart + outval
  CALC outval = Spart + outval
	CALC outval = outval + Kbias
	CALC outval = outval / factor
  TEST outval < minoutval
    SET outval minoutval
  ELSE
    TEST outval > maxoutval
      SET outval maxoutval
    ENDTEST
  ENDTEST
  CALC outval = dir * outval
	TEST powerok = 1
		SET brakerelease 1
	ELSE
		SET brakerelease 0
		SET homed 0
	ENDTEST
	TEST staticmode = 1
		SET Motor SlowStopSpeed
		TEST setpoint = oldsetpoint
			SET brakerelease 0
      TEST TimeDisable = 1
        SET motorenable 0
      ENDTEST
		ELSE
			SET brakerelease 1
      SET motorenable 1
			SET staticmode 0
			TIMEOUT TimeStatic ST_RUN
			SETSTATE ST_RUN
		ENDTEST
		SET oldsetpoint setpoint
	ELSE
		SET Motor outval
		TEST TimeStatic = 1
			TEST setpoint = oldsetpoint
      AND poserr < 200
				SET staticmode 1
				SET brakerelease 0
			  TIMEOUT TimeDisable ST_RUN
			  SETSTATE ST_RUN
			ELSE
				TEST powerok = 1
					SET brakerelease 1
          SET motorenable 1
				ENDTEST
			ENDTEST
			SET oldsetpoint setpoint
			TIMEOUT TimeStatic ST_RUN
			SETSTATE ST_RUN
		ENDTEST
	ENDTEST
  TEST CamGen = ST_IDLE
		SET brakerelease 0
		TIMEOUT TimeSlowStop ST_SLOWSTOP
		SETSTATE ST_SLOWSTOP
  ENDTEST
END

State=ST_STARTHOME
	SET homed 0
;  SET encoderreset 6
	SET brakerelease 0
	SET homeclicks 0
	TIMEOUT TimeHomeStop ST_HOME
	SETSTATE ST_HOME
END

State=ST_HOME 
	SET encoderreset 2
	SET Motor 0.0
	TEST homeclicks >= maxhomeclicks
		SET motorenable 1
		SETSTATE ST_ENDHOME
	ENDTEST
  TEST TimeHomeStop = 1
		SET brakerelease 1
		TIMEOUT TimeHomeGo ST_HOME2
		SETSTATE ST_HOME2
  ENDTEST
END

State=ST_HOME2
  TEST TimeHomeGo = 1
		SET brakerelease 0
		CALC homeclicks = homeclicks + 1
		TIMEOUT TimeHomeStop ST_HOME
		SETSTATE ST_HOME
  ENDTEST
END

State=ST_ENDHOME
	SET encoderreset 2
	SET brakerelease 1
	SET Motor motorstartval
	TIMEOUT TimeHomeEnd ST_ENDHOME2
  SETSTATE ST_ENDHOME2
END

State=ST_ENDHOME2
	SET encoderreset 6
	SET Motor 0.0
  SET brakerelease 1
	TEST TimeHomeEnd = 1
		SET homed 1
		TIMEOUT TimeStatic ST_RUN
		SETSTATE ST_RUN
	ENDTEST
END
