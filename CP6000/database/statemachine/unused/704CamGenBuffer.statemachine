STATEMACHINEVERSION 3
Name=CamGenBuffer
Delaystop=10000000
Link=PID PIDBuffer
Link=Master BufferPlads
Link=parent Parent
linkValue=encoder PID
linkValue=ticksround PID
Const=posBack 300
Const=posForward 200
Const=tTimeForward 600000
Const=tTimeBack 700000
Value=setspeed 0
Value=setpoint 0
Value=beginpos 0.0
Value=endpos 0.0
Value=beginspeed 0.0
Value=endspeed 0.0
Value=ticksroundhalf 0
Value=ticksroundquart 0
Value=curpos 0
Value=curt 0
Value=temp 0
Value=ga 0.0
Value=gb 0.0
Value=gc 0.0
Value=gd 0.0
Value=xtot 0.0
Value=curpm 0.0
Value=millitot 0.0
Value=curpos 0.0
Value=curspeed 0.0
Value=tcpm 0.0
Value=curt 0
Timeout=ttot 5000000
Const=testmode 0
Value=teststate 0
Timeout=TimeTest 2000000

State=ST_HALT
END

State=ST_TIMER
END

State=ST_IDLE
	TEST parent = ST_RESET
		SETSTATE ST_RESET
	ENDTEST
END

State=ST_RESET
	SET setpoint encoder
  SETSTATE ST_NOCHOOSETRACK
END

State=ST_NOCHOOSETRACK
	TEST PID = ST_RUN
		SET setpoint encoder
		SETSTATE ST_CHOOSETRACK
	ENDTEST
	TEST parent = ST_IDLE
		SETSTATE ST_IDLE
	ENDTEST
END

State=ST_CHOOSETRACK
	SET beginspeed 0.0
	SET endspeed 0.0
  TEST PID = ST_RUN
    CALC ticksroundhalf = ticksround / 2
    TEST Master = ST_BEGINCLOSE
      SET ttot tTimeBack
      CALC beginpos = encoder % ticksround
      CALC ticksroundquart = ticksroundhalf / 2
      TEST beginpos > ticksroundquart
        CALC endpos = ticksround + posBack
      ELSE
        SET endpos posForward
      ENDTEST
      SETSTATE ST_START
    ELSE
      TEST Master = ST_BEGINOPEN
        SET ttot tTimeForward
        CALC beginpos = encoder + ticksroundhalf
        CALC beginpos = beginpos % ticksround
        CALC beginpos = beginpos - ticksroundhalf
        CALC endpos = ticksroundhalf + posForward
        SETSTATE ST_START
			ELSE
				TEST testmode = 1
					TEST teststate = 0
						SET teststate 1
						SET ttot tTimeForward
						CALC beginpos = encoder + ticksroundhalf
						CALC beginpos = beginpos % ticksround
						CALC beginpos = beginpos - ticksroundhalf
						CALC endpos = ticksroundhalf + posForward
						TIMEOUT TimeTest ST_START
						SETSTATE ST_TIMER
					ELSE
						TEST teststate = 1
							SET teststate 0
							SET ttot tTimeBack
							CALC beginpos = encoder % ticksround
							CALC ticksroundquart = ticksroundhalf / 2
							TEST beginpos > ticksroundquart
								CALC endpos = ticksround + posBack
							ELSE
								SET endpos posForward
							ENDTEST
							TIMEOUT TimeTest ST_START
							SETSTATE ST_TIMER
						ENDTEST
					ENDTEST
				ENDTEST
			ENDTEST
		ENDTEST
	ELSE
		SET setspeed 0
		SETSTATE ST_NOCHOOSETRACK
  ENDTEST
  TEST Master = ST_IDLE
    SETSTATE ST_IDLE
  ENDTEST
END

State=ST_STOP
  SET curpm 1000
  SET setpoint endpos
	SET setspeed 0
  SETSTATE ST_CHOOSETRACK
END

State=ST_START
  SET setpoint beginpos
	SET setspeed 0
  CALC xtot = endpos - beginpos
  SET curpm 0.0
  TEST xtot < 100.0
  AND xtot > -100.0
		SET setpoint endpos
	  SETSTATE ST_STOP
  ELSE
		TIMEOUT ttot ST_RUN
		SETSTATE ST_RUN
  ENDTEST
END

; position is in ticks
; begin/end speeds are in ticks/sec.
; setspeed unit is in ticks/sec.

State=ST_RUN
  SET curt ttot
  CALC curt = ttot - curt
	CALC millitot = ttot / 1000.0
	CALC curpm = curt / millitot
	;	PRINT curpm
	CALC curpos = 2000.0 * beginpos
	CALC tcpm = curpm - 1000.0
	CALC ga = tcpm * tcpm
	CALC gb = 500.0 + curpm
	CALC ga = ga * gb
	CALC curpos = curpos * ga
	CALC ga = curpm - 1500.0
  CALC ga = ga * curpm
	CALC ga = ga * endpos
	CALC ga = ga * -2000.0
	CALC gb = tcpm * millitot
	CALC gc = tcpm * beginspeed
	CALC gd = curpm * endspeed
	CALC gc = gc + gd
	CALC gb = gb * gc
	CALC ga = ga + gb
	CALC ga = ga * curpm
	CALC curpos = curpos + ga
	CALC curpos = curpos / 1000000000000.0
	SET setpoint curpos
	CALC ga = beginpos - endpos
	CALC ga =  ga * curpm
	CALC ga = ga * 6000.0
	CALC gb = curpm * 3.0
	CALC gb = gb - 1000.0
	CALC gc = gb - 1000.0
	CALC gb = gb * millitot
	CALC gb = gb * beginspeed
	CALC curspeed = ga + gb
	CALC curspeed = curspeed * tcpm
	CALC gc = gc * millitot
	CALC gc = gc * endspeed
	CALC gc = gc * curpm
	CALC curspeed = curspeed + gc
	CALC curspeed = curspeed / 1000000.0
	CALC curspeed = curspeed / millitot
	SET setspeed curspeed
	; ved samplerate p√• ca. 126 Hz 
  TEST ttot < 3800
    SETSTATE ST_STOP
  ENDTEST
END

